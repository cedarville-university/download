      SUBROUTINE DL.OPEN.FILE(F.FILE, FILE.NAME, ON.ERROR.RETURN, ERROR.MSG)
*
* INFO/BASIC SUBROUTINE
* 11/18/88
* DWS
* TITLE -----DL.OPEN.FILE
*
*
* PURPOSE: ONLY OPEN EACH FILE ONCE DURING A SESSION
*          STORE OPEN FILE.VARS IN COMMON
*
*  NOTE THAT FILES ARE OPENED FOR READ-ONLY ACCESS ON UNIDATA SYSTEMS
*
*
* Stamped: p15 rotmand, /usr/local/download, user #3248, 11 Sep 13, 07:48AM.
* Version 7.42
*
*
*************************************************************************
*

      EQU OPEN.FILE.MAX.FILES TO 200
      COMMON/DLOPEN/ DLOPEN.NUM.FILES,
      DLOPEN.FILE.NAME.LIST,
      DLOPEN.FILE.VARS(OPEN.FILE.MAX.FILES)

*
*
* ON.ERROR.RETURN USED TO DETERMINE WHAT HAPPENS IF THERE IS AN ERROR
*   = 0, SET ERROR.MSG AND STOPS
*   # 0, SET ERROR.MSG AND RETURN
*
*
* STEPS TO CHECK:
*  1. CHECK IF FILE.VAR PASSED IN ALREADY ASSIGNED TO OPEN FILE
*  2. CHECK IF FILE.NAME IS IN LIST OF FILES OPENED BY THIS SUBR
*  3. OPEN THE FILE & STORE IT IN THE LIST
*


      ERROR.MSG = ''
      TEST.FILE.NAME = TRIM(FILE.NAME)

*
* STEP 1:
*
      IF FILEINFO(F.FILE,0) = 1 THEN
         RETURN
      END


*
* STEP 2:
*
      LOCATE TEST.FILE.NAME IN DLOPEN.FILE.NAME.LIST<1> SETTING POS THEN
         F.FILE = DLOPEN.FILE.VARS(POS)
         RETURN
      END


*
* STEP 3:
*
* IF OUR ARRAY IS FULL, WE DISCARD THE FIRST ENTRY IN THE LIST
      IF DLOPEN.NUM.FILES >= OPEN.FILE.MAX.FILES THEN
         DLOPEN.NUM.FILES = 0
      END
*
      FILE.OPEN.OKAY = 0
      IF TEST.FILE.NAME[1,5] = 'DICT ' THEN
         DATA.FILE.NAME = TEST.FILE.NAME[6,80]
         OPEN 'DICT', DATA.FILE.NAME READONLY TO F.FILE THEN
            FILE.OPEN.OKAY = 1
         END
      END ELSE
         OPEN '', TEST.FILE.NAME READONLY TO F.FILE THEN
            FILE.OPEN.OKAY = 1
         END
      END
      IF FILE.OPEN.OKAY THEN
         DLOPEN.NUM.FILES += 1
         DLOPEN.FILE.NAME.LIST<DLOPEN.NUM.FILES> = TEST.FILE.NAME
         DLOPEN.FILE.VARS(DLOPEN.NUM.FILES) = F.FILE
      END ELSE
         ERROR.MSG = '** CAN NOT OPEN "':FILE.NAME:'" IN "DL.OPEN.FILE"'
         IF NOT(ON.ERROR.RETURN) THEN
            STOP '"':FILE.NAME:'" NOT FOUND'
         END ELSE
            RETURN
         END
      END
      RETURN


   END
