      FUNCTION DOWNLOAD.FORMAT.XML(A.INPUT.STRING,A.USE.CDATA)
*
* UNIBASIC FUNCTION
* 09/15/2003
* DLR
* TITLE ----- DOWNLOAD.FORMAT.XML
*
*
* MODULE :
* PURPOSE: HANDLE ILLEGAL XML CHARACTERS
*
* Stamped: p5 rotmand, /datatel/live/collive, user #8225, 15 Sep 03, 08:44AM.
*
*************************************************************************
*

      GOSUB SET.UP
      X.RETURN.STRING = A.INPUT.STRING
      X.USE.CDATA = OCONV(A.USE.CDATA,'MCU')
      IF (X.USE.CDATA = 'Y') OR (X.USE.CDATA = @TRUE) THEN
         GOSUB USE.CDATA
      END ELSE
         GOSUB USE.ESCAPE
      END
      RETURN X.RETURN.STRING


SET.UP: 
      X.XML.CDATA.START = '<![CDATA['
      X.XML.CDATA.END = ']]>'
* NOTE THAT THE AMPERSAND MUST BE DONE FIRST.  OTHERWISE, THE
* ESCAPED STRINGS WILL LOOK LIKE AMPERSANDS IN THE ORIGINAL DATA
      XL.ILLEGAL.CHARS = '& < > "'
      XL.ILLEGAL.CHARS := " '"
      CONVERT ' ' TO @VM IN XL.ILLEGAL.CHARS
      STRING.ILLEGAL.CHARS = XL.ILLEGAL.CHARS
      CONVERT @VM TO '' IN STRING.ILLEGAL.CHARS
      NUM.XL.ILLEGAL.CHARS = DCOUNT(XL.ILLEGAL.CHARS,@VM)
      XL.REPLACEMENT = '&amp; &lt; &gt; &quot; &apos;'
      CONVERT ' ' TO @VM IN XL.REPLACEMENT
      RETURN


USE.CDATA: 
      X.TEMP = A.INPUT.STRING
      CONVERT STRING.ILLEGAL.CHARS TO '' IN X.TEMP
      IF LEN(X.TEMP) NE LEN(A.INPUT.STRING) THEN
         X.RETURN.STRING = X.XML.CDATA.START:X.RETURN.STRING:X.XML.CDATA.END
      END
      RETURN


USE.ESCAPE: 
      FOR WHICH.XL.ILLEGAL.CHARS = 1 TO NUM.XL.ILLEGAL.CHARS
         X.ILLEGAL.CHARS = XL.ILLEGAL.CHARS<1,WHICH.XL.ILLEGAL.CHARS>
         X.REPLACEMENT = XL.REPLACEMENT<1,WHICH.XL.ILLEGAL.CHARS>
         SWAP X.ILLEGAL.CHARS WITH X.REPLACEMENT IN X.RETURN.STRING
      NEXT WHICH.XL.ILLEGAL.CHARS
      RETURN


   END
