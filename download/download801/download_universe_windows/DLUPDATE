      SUBROUTINE DOWNLOAD.UPDATE(BY.EXP.VALUE.CTR, BY.EXP.SUBVALUE.CTR)
*
* INFO/BASIC SUBROUTINE
* 11/27/90
* DWS
* TITLE ----- DOWNLOAD.UPDATE
*
*
* PURPOSE: UPDATE TOTALS/COUNTS, MOVE NEW.xxx.ITEMS TO xxx.ITEMS
*
*
*************************************************************************
*
* Stamped: p12 rotmand, /usr/local/download, user #2048, 29 Sep 15, 07:41AM.
* Version 8.01
*
********************************************************************
*

      $INCLUDE IWHICHOPSYS
      $INCLUDE IDLMAX
      $INCLUDE IDLMAIN
      $INCLUDE IDLDATA
      $INCLUDE IDLWHEN
      $INCLUDE IDLOUTREC


      DEBUG1OR2
      CRT 'INSIDE DOWNLOAD.UPDATE'
      END

*
*
* MAIN CONTROL
*
      FOR DICT.FIELD.CTR=1 TO DL.DATA.NUM.DICT.RECS
         PART.OF.ASSOC.VALUE = DL.DATA.DICT.PART.OF.ASSOC.LIST(DICT.FIELD.CTR)
         IF BY.EXP.FIELD # '' AND PART.OF.ASSOC.VALUE THEN
            DATA.FIELD = DL.DATA.DATA.ITEMS(DICT.FIELD.CTR)
            DATA.VALUE = DATA.FIELD<1,BY.EXP.VALUE.CTR,BY.EXP.SUBVALUE.CTR>
            GOSUB UPDATE.VALUES
         END ELSE
            DATA.VALUE = DL.DATA.DATA.ITEMS(DICT.FIELD.CTR)
            GOSUB UPDATE.VALUES
         END
      NEXT DICT.FIELD.CTR
      RETURN



*
*
* UPDATE TOTALS/COUNTS/ETC.
*
UPDATE.VALUES: 
      IF DICT.KEEP.AVERAGE(DICT.FIELD.CTR) THEN
         GOSUB UPDATE.COUNT
      END
      IF DL.DATA.DICT.KEEP.TOTAL(DICT.FIELD.CTR) OR DICT.KEEP.AVERAGE(DICT.FIELD.CTR) THEN
         GOSUB UPDATE.TOTAL
      END
      IF DICT.KEEP.MIN(DICT.FIELD.CTR) THEN
         GOSUB UPDATE.MIN
      END
      IF DICT.KEEP.MAX(DICT.FIELD.CTR) THEN
         GOSUB UPDATE.MAX
      END
      RETURN


*
*
* UPDATE COUNT
*
UPDATE.COUNT: 
      FOR BREAK.FIELD.CTR=1 TO DLMAIN.NUM.BREAK.FIELDS
         IF DATA.INCLUDE.NULLS(BREAK.FIELD.CTR) OR DATA.VALUE#'' THEN
            DATA.COUNT(BREAK.FIELD.CTR)<DICT.FIELD.CTR> += 1
         END
      NEXT BREAK.FIELD.CTR
      RETURN


*
*
* UPDATE TOTAL
*
UPDATE.TOTAL: 
      FOR BREAK.FIELD.CTR=1 TO DLMAIN.NUM.BREAK.FIELDS
         DATA.TOTAL(BREAK.FIELD.CTR)<DICT.FIELD.CTR> += DATA.VALUE
      NEXT BREAK.FIELD.CTR
      RETURN


*
*
* UPDATE MIN
*
UPDATE.MIN: 
      FOR BREAK.FIELD.CTR=1 TO DLMAIN.NUM.BREAK.FIELDS
         OLD.MIN = DATA.MIN(BREAK.FIELD.CTR)<DICT.FIELD.CTR>
         IF DATA.VALUE < OLD.MIN THEN
            DATA.MIN(BREAK.FIELD.CTR)<DICT.FIELD.CTR> = DATA.VALUE
         END
      NEXT BREAK.FIELD.CTR
      RETURN


*
*
* UPDATE MAX
*
UPDATE.MAX: 
      FOR BREAK.FIELD.CTR=1 TO DLMAIN.NUM.BREAK.FIELDS
         OLD.MAX = DATA.MAX(BREAK.FIELD.CTR)<DICT.FIELD.CTR>
         IF DATA.VALUE > OLD.MAX THEN
            DATA.MAX(BREAK.FIELD.CTR)<DICT.FIELD.CTR> = DATA.VALUE
         END
      NEXT BREAK.FIELD.CTR
      RETURN

   END
