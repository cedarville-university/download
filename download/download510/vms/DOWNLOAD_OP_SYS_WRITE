      SUBROUTINE DOWNLOAD.OP.SYS.WRITE(PRINT.LINE, FLUSH.BUFFER)
*
* INFO/BASIC SUBROUTINE
* 11/27/90
* DWS
* TITLE ----- DOWNLOAD.OP.SYS.WRITE
*
*
* PURPOSE: WRITE A RECORD TO A "DIRECT" FILE (DIFFERENT FOR EACH OP.SYS VALUE)
*
* Stamped: pe rotmand, /disk1/coltest, user #12980, 01 Feb 01, 08:04AM.
* Version 5.10
*
*************************************************************************
*

$INCLUDE I_DOWNLOAD_MAX_COMMON
$INCLUDE I_DOWNLOAD_MAIN_COMMON
$INCLUDE I_DOWNLOAD_OP_SYS_COMMON
$INCLUDE I_WHICH_OP_SYS

      BEGIN CASE
         CASE OP.SYS = 'PRIMOS'
            GOSUB OP.SYS.PRIMOS.WRITE.FILE
         CASE OP.SYS = 'UNIX'
            GOSUB OP.SYS.UNIX.WRITE.FILE
      END CASE
      IF FLUSH.BUFFER THEN
         BEGIN CASE
            CASE OP.SYS = 'PRIMOS'
               GOSUB OP.SYS.PRIMOS.FLUSH.FILE
            CASE OP.SYS = 'UNIX'
               GOSUB OP.SYS.UNIX.FLUSH.FILE
         END CASE
      END
      RETURN


*
*
* DO A PRIMOS WRITE DIRECTLY TO FILE
*
OP.SYS.PRIMOS.WRITE.FILE:
      OP.SYS.BUFFER := PRINT.LINE
      LOOP
         LEN.OP.SYS.BUFFER = LEN(OP.SYS.BUFFER)
      UNTIL LEN.OP.SYS.BUFFER < OP.SYS.BUFFER.SIZE
         BUFFER.TO.WRITE = OP.SYS.BUFFER[1,OP.SYS.BUFFER.SIZE]
         OP.SYS.BUFFER = OP.SYS.BUFFER[OP.SYS.BUFFER.SIZE+1,LEN.OP.SYS.BUFFER]
         WORDS.WRITTEN = 0
         WRITE.ERRMSG = ''
         WORDS.TO.WRITE = OP.SYS.BUFFER.SIZE / 2
         GOSUB OP.SYS.PRIMOS.WRITE.BUFFER
      REPEAT
      RETURN


*
*
* DO THE ACTUAL CALL TO PRIMOS.WRITE.FILE WITH THE CONTENTS OF THE BUFFER
*
OP.SYS.PRIMOS.WRITE.BUFFER:
      WORDS.WRITTEN = 0
      WRITE.ERRMSG = ''
      CALL PRIMOS.WRITE.FILE(OP.SYS.FILE.FUNIT, WORDS.TO.WRITE, BUFFER.TO.WRITE, WORDS.WRITTEN, WRITE.ERRMSG)
      IF WRITE.ERRMSG THEN
         DLMAIN.ERRMSG<-1> = 'Can not write to "':OP.SYS.FILE.PATH.NAME:' for record "':@ID:'"'
         DLMAIN.ERRMSG<-1> = '   ':WRITE.ERRMSG
         DLMAIN.FATAL.ERROR = TRUE
      END ELSE
         IF WORDS.WRITTEN # WORDS.TO.WRITE THEN
            DLMAIN.ERRMSG<-1> = 'Error doing direct write to "':OP.SYS.FILE.PATH.NAME:'" for record "':@ID:'"'
            DLMAIN.ERRMSG<-1> = '   Expecting to write ':OP.SYS.BUFFER.SIZE:' characters but wrote ':(WORDS.WRITTEN*2):' characters.'
         END
      END
      RETURN

OP.SYS.UNIX.WRITE.FILE:
COMMENT.OUT.UNIX      OSBWRITE PRINT.LINE ON OP.SYS.FILE.FUNIT AT OP.SYS.BUFFER.SIZE
      OP.SYS.BUFFER.SIZE += LEN(PRINT.LINE)
      RETURN


*
*
* FINISH UP WHATEVER IS IN THE BUFFER
*   IF THERE IS AN ODD NUMBER OF CHARACTERS LEFT TO WRITE,
*   PAD THE END WITH THE NULL CHARACTER 0
*
OP.SYS.PRIMOS.FLUSH.FILE:
      LEN.OP.SYS.BUFFER = LEN(OP.SYS.BUFFER)
      BUFFER.TO.WRITE = OP.SYS.BUFFER
      WORDS.TO.WRITE = INT(LEN.OP.SYS.BUFFER / 2)
      IF (WORDS.TO.WRITE*2) # LEN.OP.SYS.BUFFER THEN
         OP.SYS.BUFFER := OP.SYS.CHAR.ZERO
         WORDS.TO.WRITE += 1
      END
      GOSUB OP.SYS.PRIMOS.WRITE.BUFFER
      RETURN

*
* EACH RECORD IS WRITTEN AS A BLOCK, NO BUFFERING OR FLUSHING
* IS NEEDED
*
OP.SYS.UNIX.FLUSH.FILE:
      RETURN

   END
