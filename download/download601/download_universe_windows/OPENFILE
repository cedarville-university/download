      SUBROUTINE OPENFILE(F.FILE, FILE.NAME, ON.ERROR.RETURN, ERROR.MSG)
*
* INFO/BASIC SUBROUTINE
* 11/18/88
* DWS
* TITLE -----OPENFILE
*
*
* PURPOSE: ONLY OPEN EACH FILE ONCE DURING A SESSION
*          STORE OPEN FILE.VARS IN COMMON
*
* Stamped: p7 rotmand, /user/local/collive, user #13273, 29 Jan 03, 04:46PM.
* Version 6.01
*
*
*
*
*
*
*     Last updated by LIVE (ROTMAND) at 15:58:11 on 02/12/1993.
* Call our own version of FATAL.ERROR so that we don't leave
* the terminal in half duplex.
*     Last updated by LIVE (SJOQUIST) at 08:43:02 on 11/18/1988.
*
*************************************************************************
*

      EQU OPENFILE.MAX.FILES TO 200
      COMMON/OPENFIL/ OPENFIL.NUM.FILES,
      OPENFIL.FILE.NAME.LIST,
      OPENFIL.FILE.VARS(OPENFILE.MAX.FILES)

*
*
* ON.ERROR.RETURN USED TO DETERMINE WHAT HAPPENS IF THERE IS AN ERROR
*   = 0, SET ERROR.MSG AND STOPS
*   # 0, SET ERROR.MSG AND RETURN
*
*
* STEPS TO CHECK:
*  1. CHECK IF FILE.VAR PASSED IN ALREADY ASSIGNED TO OPEN FILE
*  2. CHECK IF FILE.NAME IS IN LIST OF FILES OPENED BY THIS SUBR
*  3. OPEN THE FILE & STORE IT IN THE LIST
*


      ERROR.MSG = ''
      TEST.FILE.NAME = TRIM(FILE.NAME)

*
* STEP 1:
*
* IF @LOGNAME = 'rotmand' THEN CRT 'FILE ':FILE.NAME
      IF FILEINFO(F.FILE,0) = 1 THEN
* IF @LOGNAME = 'rotmand' THEN CRT '   ALREADY OPEN'
         RETURN
      END


*
* STEP 2:
*
      LOCATE TEST.FILE.NAME IN OPENFIL.FILE.NAME.LIST<1> SETTING POS THEN
         F.FILE = OPENFIL.FILE.VARS(POS)
         RETURN
      END


*
* STEP 3:
*
* IF OUR ARRAY IS FULL, WE DISCARD THE FIRST ENTRY IN THE LIST
      IF OPENFIL.NUM.FILES >= OPENFILE.MAX.FILES THEN
         OPENFIL.NUM.FILES = 0
      END
*
      FILE.OPEN.OKAY = 0
      IF TEST.FILE.NAME[1,5] = 'DICT ' THEN
         DATA.FILE.NAME = TEST.FILE.NAME[6,80]
         OPEN 'DICT', DATA.FILE.NAME TO F.FILE THEN
            FILE.OPEN.OKAY = 1
         END
      END ELSE
         OPEN '', TEST.FILE.NAME TO F.FILE THEN
            FILE.OPEN.OKAY = 1
         END
      END
      IF FILE.OPEN.OKAY THEN
         OPENFIL.NUM.FILES += 1
         OPENFIL.FILE.NAME.LIST<OPENFIL.NUM.FILES> = TEST.FILE.NAME
         OPENFIL.FILE.VARS(OPENFIL.NUM.FILES) = F.FILE
      END ELSE
         ERROR.MSG = '** CAN NOT OPEN "':FILE.NAME:'" IN "OPENFILES"'
         IF NOT(ON.ERROR.RETURN) THEN
            STOP '"':FILE.NAME:'" NOT FOUND'
         END ELSE
            RETURN
         END
      END
      RETURN


   END
