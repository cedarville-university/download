143 LINES
*-------------------------- FTP SUBMISSION FORM --------------------------
*--- PROGRAM NAME...: X.REBUILD.ASSOC
*--- 40-CHAR DESC...: Rebuilds association phrases for UniData files
*--- KEYWORDS.......:
*--- SUBMISSION DATE: 07-02-96
*--- COMPANY NAME...: Hardin-Simmons University
*--- CONTACT NAME...: Thomas Mantooth
*--- CONTACT PHONE#.: 915-670-1288
*--- CONTACT EMAIL..: mantooth.compsvc@hsutx.edu
*--- LONG DESC......: This utility will rebuild the phrases for all
*---                : associations in a UniData dictionary.  It will
*---                : process a single file or use a savedlist of files.
*---                :
*---                :
*-------------------------------------------------------------------------
FILE/RECORD INFORMATION:
  1 USERS.LIB.......................... X.REBUILD.ASSOC               
 ##### FILE USERS.LIB #############################    1 RECORDS ######
 ======== RECORD X.REBUILD.ASSOC ==================   119 FIELDS ======
 PROGRAM X.REBUILD.ASSOC
 PROMPT ""
 
 *   Define name of program 
 
 PROGRAM.NAME = "X.REBUILD.ASSOC"
 
 *   If a select list is active, use filenames from it
 
 X.SELECT.RC = SELECTINFO(0,1)
 IF  X.SELECT.RC # 1  THEN
 
     *   Get parameters, whether passed on command line or in paragraph
 
     COMMAND.LINE = CONVERT(" ",@FM,TRIM(@COMMAND))
     COMMAND.WORD = UPCASE(COMMAND.LINE<1>)
     IF  COMMAND.WORD # PROGRAM.NAME  THEN
         COMMAND.LINE = CONVERT(" ",@FM,TRIM(@SENTENCE))
     END
 
     *   Get name of file to use and make sure it is valid
 
     X.FILE = COMMAND.LINE<2>
 
     LOOP
         OPEN "", X.FILE TO F.FILE  ELSE
             X.FILE = ""
         END
       WHILE  NOT(X.FILE)  DO
         PRINT "Enter name of DICT to process: ":
         INPUT X.FILE
 
         *   Provide an escape in case user wants to exit program
 
         TEMP = UPCASE(X.FILE)
         IF  TEMP = "STOP" OR TEMP = "QUIT" OR TEMP = "END"  THEN
             STOP "Aborting at operator request."
         END
     REPEAT
 END
 
 IF  X.FILE  THEN
     GOSUB REBUILD.ASSOC
   END ELSE
     EOF = 0
     LOOP
         READNEXT X.FILE  ELSE
             EOF = 1
         END
       UNTIL EOF  DO
         GOSUB REBUILD.ASSOC
     REPEAT
 END
 
 RETURN
 
 *------------------------------------------------------------------*
 
 REBUILD.ASSOC:
  
 PRINT "Processing DICT ":X.FILE
 
 EXEC.CMD = "SELECT DICT ":X.FILE:" WITH ASSOC SAVING UNIQUE ASSOC TO 1
C"
 EXECUTE "HUSH ON"
 EXECUTE EXEC.CMD
 
 X.SELECT.RC = SELECTINFO(1,1)
 IF  X.SELECT.RC # 1  THEN
     EXECUTE "HUSH OFF"
     PRINT "No DICT items with ASSOCiations for ":X.FILE:"."
     RETURN
 END
 
 OPEN "DICT",X.FILE TO F.DICT  ELSE
     EXECUTE "HUSH OFF"
     PRINT "Cannot open DICT ":X.FILE:"-Skipping file."
     RETURN
 END
 
 EOF.ASSOC = 0
 LOOP
     READNEXT ASSOC.NAME FROM 1  ELSE
         EOF.ASSOC = 1
     END
     IF  NOT(ASSOC.NAME)  THEN
         EOF.ASSOC = 1
       END ELSE
         ASSOC.NAME = TRIM(ASSOC.NAME)
         IF  NOT(ASSOC.NAME)  THEN
             CONTINUE
         END
     END
   UNTIL EOF.ASSOC  DO
     DICT.FLAG = 0
     EXEC.CMD  = "SELECT DICT ":X.FILE:" WITH ASSOC = '":ASSOC.NAME:"' 
C"
     EXEC.CMD := "BY TYPE BY LOC.KEY TO 2"
     PRINT EXEC.CMD
     EXECUTE EXEC.CMD
     X.SELECT.RC = SELECTINFO(2,1)
     IF  X.SELECT.RC = 1  THEN
         DICT.FLAG = 1
         EOF.DICT = 0
         PHRASE = "PH"
         READLIST X.DICT.ITEMS FROM 2  ELSE
             X.DICT.ITEMS = ""
         END
         CONVERT @FM TO " " IN X.DICT.ITEMS
         PHRASE<2> = TRIM(X.DICT.ITEMS)
     END
     IF  DICT.FLAG  THEN
         WRITE PHRASE ON F.DICT, ASSOC.NAME
     END
 REPEAT
 
 CLOSE F.DICT
 
 RETURN
 
 END
 ======== END OF RECORD ===============================================
 ### END OF FILE ######################################################
