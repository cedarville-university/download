      FUNCTION FORMAT.XML(A.INPUT.STRING)
*
* UNIBASIC FUNCTION
* 09/15/2003
* DLR
* TITLE ----- FORMAT.XML
*
*
* MODULE :
* PURPOSE: HANDLE ILLEGAL XML CHARACTERS
*
* Stamped: pts_6 rotmand, /datatel/coll18/live/apphome, user #12238, 14 Sep 11, 07:37AM.
*    Replace pound sign (#) with &#35;
* Stamped: pts_66 rotmand, /datatel/coll18/live/apphome, user #11517, 21 Apr 10, 01:22PM.
*    Remove ASCII characters less than decimal 32 (non-printing characters)
* Stamped: pts_14 rotmand, /datatel/coll18/live/apphome, user #7024, 20 Apr 10, 11:09AM.
*    Convert @SM and @FM to spaces (@VM were already being converted)
* Stamped: pty_ttysc voumardb, /datatel/live/collive, user #21608, 18 Nov 04, 01:32PM.
*    Convert value marks in input string to spaces before formatting
* Stamped: p5 rotmand, /datatel/live/collive, user #8225, 15 Sep 03, 08:44AM.
*
*************************************************************************
*

      X.USE.CDATA = @FALSE
      IF @LOGNAME = '*rotmand' THEN
         X.USE.CDATA = @TRUE
      END
      GOSUB SET.UP
      X.RETURN.STRING = A.INPUT.STRING
      CONVERT @SM:@VM:@FM TO '   ' IN X.RETURN.STRING
      CONVERT XL.NON.PRINTING TO '' IN X.RETURN.STRING
*DLR*      CONVERT ';[]?' TO '    ' IN X.RETURN.STRING
      IF X.USE.CDATA THEN
         GOSUB USE.CDATA
      END ELSE
         GOSUB USE.ESCAPE
      END
      RETURN X.RETURN.STRING


SET.UP: 
      X.XML.CDATA.START = '<![CDATA['
      X.XML.CDATA.END = ']]>'
* NOTE THAT THE AMPERSAND MUST BE DONE FIRST.  OTHERWISE, THE
* ESCAPED STRINGS WILL LOOK LIKE AMPERSANDS IN THE ORIGINAL DATA
      XL.XML.ILLEGAL.CHARS = '& < > " #'
      XL.XML.ILLEGAL.CHARS := " '"
      CONVERT ' ' TO @VM IN XL.XML.ILLEGAL.CHARS
      STRING.ILLEGAL.CHARS = XL.XML.ILLEGAL.CHARS
      CONVERT @VM TO '' IN STRING.ILLEGAL.CHARS
      NUM.XL.XML.ILLEGAL.CHARS = DCOUNT(XL.XML.ILLEGAL.CHARS,@VM)
      XL.XML.REPLACEMENT = '&amp; &lt; &gt; &quot; &#35; &apos;'
      CONVERT ' ' TO @VM IN XL.XML.REPLACEMENT
      XL.NON.PRINTING = ''
      FOR WHICH.NON.PRINTING = 1 TO 31
         XL.NON.PRINTING := CHAR(WHICH.NON.PRINTING)
      NEXT WHICH.NON.PRINTING
      RETURN


USE.CDATA: 
      X.TEMP = A.INPUT.STRING
      CONVERT STRING.ILLEGAL.CHARS TO '' IN X.TEMP
      IF LEN(X.TEMP) NE LEN(A.INPUT.STRING) THEN
         X.RETURN.STRING = X.XML.CDATA.START:X.RETURN.STRING:X.XML.CDATA.END
      END
      RETURN


USE.ESCAPE: 
      FOR WHICH.XL.XML.ILLEGAL.CHARS = 1 TO NUM.XL.XML.ILLEGAL.CHARS
         X.XML.ILLEGAL.CHARS = XL.XML.ILLEGAL.CHARS<1,WHICH.XL.XML.ILLEGAL.CHARS>
         X.XML.REPLACEMENT = XL.XML.REPLACEMENT<1,WHICH.XL.XML.ILLEGAL.CHARS>
         SWAP X.XML.ILLEGAL.CHARS WITH X.XML.REPLACEMENT IN X.RETURN.STRING
      NEXT WHICH.XL.XML.ILLEGAL.CHARS
      RETURN


   END
