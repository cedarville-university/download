      SUBROUTINE UNPACK.DECIMAL(RETURN.STRING,ERROR.MSG,PASSED.FIELD)
*
* INFO/BASIC SUBROUTINE
* 05/31/92
* DLR
* TITLE ----- UNPACK.DECIMAL
*
*
* MODULE :
* PURPOSE: RETURN INFO/BASIC NUMERIC STRING VALUE FOR PACKED-DECIMAL FIELD
*
*     Last updated by LIVE (ROTMAN) at 08:07:36 on 05/31/1992.
*
*************************************************************************
*

      RETURN.STRING = ''
      ERROR.MSG = ''
      HEX.DIGITS = '0123456789ABCDEF'

      PASSED.FIELD.LEN = LEN(PASSED.FIELD)
      FOR BYTE.CTR=1 TO PASSED.FIELD.LEN
         BYTE = SEQ(PASSED.FIELD[BYTE.CTR,1])
         NIBBLE.1 = INT(BYTE/16)
         NIBBLE.2 = BYTE - NIBBLE.1*16
         RETURN.STRING := HEX.DIGITS[NIBBLE.1+1,1]:HEX.DIGITS[NIBBLE.2+1,1]
      NEXT BYTE.CTR
      LAST.CHAR = RETURN.STRING[1]
      RETURN.STRING = RETURN.STRING[1,2*PASSED.FIELD.LEN-1]
      BEGIN CASE
         CASE NOT(NUM(RETURN.STRING))
            ERROR.MSG = 'Not numeric'
         CASE LAST.CHAR = 'C' OR LAST.CHAR = 'F'
            NULL
         CASE LAST.CHAR = 'D'
            RETURN.STRING = '-':RETURN.STRING
         CASE 1
            ERROR.MSG = 'Unidentified sign field ':LAST.CHAR
      END CASE
      RETURN

   END
