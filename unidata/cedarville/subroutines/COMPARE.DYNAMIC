      SUBROUTINE COMPARE.DYNAMIC(ITEM.A,ITEM.B,ITEM.ORDER)
*
* INFO/BASIC SUBROUTINE
* 4/14/88
* DWS
* TITLE ----- COMPARE.DYNAMIC
*
*
* PURPOSE: COMPARE TWO STRINGS, TAKING @FM,@VM,@SM INTO CONSIDERATION
*          RETURNS '<' FOR ASCENDING ORDER (ITEM.A < ITEM.B)
*                  '=' FOR EQUAL
*                  '>' FOR DESCENDING ORDER (ITEM.A > ITEM.B)
*
*
*     Last updated by LIVE (SJOQUIST) at 13:22:57 on 04/14/1988.
*
*
*
*************************************************************************
*
*  This program is provided 'as is' for your use by ROTMAN & SJOQUIST.
*  You are encouraged to verify the accuracy of program functions prior
*  to use with actual data files.
*
*  If you have questions about this program, or if you wish to obtain
*  a list of programs available for purchase from ROTMAN & SJOQUIST,
*  contact Dave Rotman or Doug Sjoquist:
*
*      Rotman and Sjoquist
*      c/o Cedarville College
*      PO Box 601
*      Cedarville, OH 45314
*      (513) 766-2211
*
*************************************************************************
*
*

*
* CHECK FOR EQUALITY
*
      IF ITEM.A = ITEM.B THEN
         ITEM.ORDER = '='
         RETURN
      END

*
* OTHERWISE, COMPARE PIECE BY PIECE
*
      ITEM.ORDER = ''
      DELIM.A = 999
      DELIM.B = 999
*
*
* SINCE THE STRINGS ARE NOT EQUAL, EITHER ONE PIECE WILL BE DIFFERENT
*  OR ONE DELIMITER WILL BE
*
      LOOP
      UNTIL ITEM.ORDER
*
* COMPARE EACH PIECE AS THEY ARE REMOVED
*
         PIECE.A = REMOVE(ITEM.A,DELIM.A)
         PIECE.B = REMOVE(ITEM.B,DELIM.B)
         BEGIN CASE
            CASE PIECE.A < PIECE.B
               ITEM.ORDER = '<'
            CASE PIECE.A > PIECE.B
               ITEM.ORDER = '>'
*
* TO TRAP SITUATION WHEN COMPARING A NULL VALUE WITH ZERO
* (THEY ARE NOT EQUAL, BUT THE < & > CHECKS FAIL
*
            CASE PIECE.A # PIECE.B
               IF PIECE.A = '' THEN
                  ITEM.ORDER = '<'
               END ELSE
                  ITEM.ORDER = '>'
               END
*
* IF THE PIECES ARE =, CHECK THE DELIMITERS
*  ( "A~B}" > "A~B" )
*   @FM = 2, @VM = 3, @SM = 4
*   IF DELIM.A = @FM AND DELIM.B = @VM THEN ITEM.A < ITEM.B
*
            CASE 1
               BEGIN CASE
                  CASE DELIM.A < DELIM.B
                     ITEM.ORDER = '<'
                  CASE DELIM.A > DELIM.B
                     ITEM.ORDER = '>'
                  CASE 1
                     NULL  ;* DELIMITERS AND PIECES ARE EQUAL...
                           ;* NEED TO MOVE ON TO NEXT PIECE
               END CASE
         END CASE
      REPEAT
      RETURN
   END
