      SUBROUTINE REMOVE.NULLS(RETURN.STRING,RECEIVED.STRING,REMOVE.TYPE)
*
* INFO/BASIC SUBROUTINE
* 1/26/89
* DLR
* TITLE ----- REMOVE.NULLS
* REL 0.0
*
*
* MODULE :
* PURPOSE: REMOVE NULLS FROM A STRING
*          IF REMOVE.TYPE = @SM, THEN WE REMOVE NULL SUBVALUES
*          IF REMOVE.TYPE = @VM, THEN WE REMOVE NULL VALUES
*          IF REMOVE.TYPE = @FM, THEN WE REMOVE NULL FIELDS
*
*     Last updated by LIVE (ROTMAN) at 06:45:43 on 01/26/1989.
*
*************************************************************************
*
* DESIGN NOTES:
* A FIELD CONSISTING OF VALUE MARKS AND SUBVALUE MARKS IS CONSIDERED
* TO BE A NULL FIELD.  SIMILARLY, A VALUE CONSISTING OF SUBVALUE MARKS
* IS CONSIDERED TO BE A NULL VALUE.
*

      DIM MARK(5)
      MARK(0) = ''
      MARK(1) = @IM
      MARK(2) = @FM
      MARK(3) = @VM
      MARK(4) = @SM
      MARK(5) = @TM

      REMOVE.SEQ = SEQ(REMOVE.TYPE)
      IF REMOVE.SEQ < 251 THEN
         RETURN.STRING = RECEIVED.STRING
         RETURN
      END
      REMOVE.DELIM = 256 - REMOVE.SEQ

      RECEIVED.STRING = RECEIVED.STRING
      RETURN.STRING = ''
      DELIM = 999
      SUBSTRING = ''
      LOOP
      UNTIL DELIM = 0
         REMOVE TOKEN FROM RECEIVED.STRING SETTING DELIM
         SUBSTRING := TOKEN
         BEGIN CASE
            CASE DELIM < REMOVE.DELIM
               IF SUBSTRING = '' THEN
                  HAVE.SOMETHING = 0
               END ELSE
                  GOSUB CHECK.SOMETHING
               END
               IF HAVE.SOMETHING THEN
                  RETURN.STRING := SUBSTRING
               END
               RETURN.STRING := MARK(DELIM)
               SUBSTRING = ''
            CASE DELIM = REMOVE.DELIM
               IF SUBSTRING = '' THEN
                  HAVE.SOMETHING = 0
               END ELSE
                  GOSUB CHECK.SOMETHING
               END
               IF HAVE.SOMETHING THEN
                  RETURN.STRING := SUBSTRING:MARK(DELIM)
               END
               SUBSTRING = ''
            CASE 1
               SUBSTRING := MARK(DELIM)
         END CASE
      REPEAT
      END.CHAR = LEN(RETURN.STRING)
      IF RETURN.STRING[END.CHAR,1] = REMOVE.TYPE THEN
         RETURN.STRING = RETURN.STRING[1,END.CHAR-1]
      END
      RETURN


CHECK.SOMETHING:
      HAVE.SOMETHING = 0
      END.CHAR = LEN(SUBSTRING)
      IF END.CHAR = 0 THEN
         RETURN
      END
      WHICH.CHAR = 0
      LOOP
      UNTIL WHICH.CHAR >= END.CHAR OR HAVE.SOMETHING
         WHICH.CHAR += 1
         CHAR.SEQ = SEQ(SUBSTRING[WHICH.CHAR,1])
         IF CHAR.SEQ < 251 THEN
            HAVE.SOMETHING = 1
         END
      REPEAT
      RETURN


   END
