      SUBROUTINE SELINP(RETURN.VALUE,VARIABLE,PROMPT.TEXT,CONV.CODE)
*
* INFO/BASIC PROGRAM
* 10/7/87
* DWS
* TITLE ----- SELINP
*
*
* PURPOSE: SUBROUTINE TO ASK FOR INPUT WHEN A SELECT IS FIRST RUN
*
* Stamped: q6 howders, /datatel/live/collive, user #25069, 28 May 03, 10:41AM.
*  Use CRT instead of PRINT
*     Last updated by LIVE (ROTMAN) at 10:06:07 on 08/11/1989.
* Echo input value to screen so that paragraphs using in-line
* prompts do not confuse the user (thinking he/she has to enter a value
* more than once).
*     Last updated by LIVE (SJOQUIST) at 11:29:42 on 05/12/1988.
* Use FIRST.RECORD & labeled common
*     Last updated by LIVE (SJOQUIST) at 08:28:58 on 10/07/1987.
*
********************************************************************
*
*
*
*************************************************************************
*
*  This program is provided 'as is' for your use by ROTMAN & SJOQUIST.
*  You are encouraged to verify the accuracy of program functions prior
*  to use with actual data files.
*
*  If you have questions about this program, or if you wish to obtain
*  a list of programs available for purchase from ROTMAN & SJOQUIST,
*  contact Dave Rotman or Doug Sjoquist:
*
*      Rotman and Sjoquist
*      c/o Cedarville College
*      PO Box 601
*      Cedarville, OH 45314
*      (513) 766-2211
*
*************************************************************************
*

      MAX.VAR = 25
      COMMON /SELINP/ NUM.VAR,
      VAR.NAME(MAX.VAR),
      VAR.VALUE(MAX.VAR)
      THIS.IS.THE.FIRST = ''
      CALL FIRST.RECORD(THIS.IS.THE.FIRST,'SELINP')
      IF THIS.IS.THE.FIRST THEN
         NUM.VAR = 0
         MAT VAR.NAME = ''
         MAT VAR.VALUE = ''
      END

      PROMPT ''
      VAR.NUM = 0
      LOOP
         VAR.NUM += 1
      UNTIL VAR.NUM > NUM.VAR OR VARIABLE = VAR.NAME(VAR.NUM)
      REPEAT

*
*  NEW VARIABLE
*
      IF VAR.NUM > NUM.VAR THEN
         NUM.VAR += 1
         VAR.NUM = NUM.VAR
         VAR.NAME(VAR.NUM) = VARIABLE

         IF PROMPT.TEXT THEN
            TEXT = PROMPT.TEXT:': '
         END ELSE
            TEXT = VARIABLE:': '
         END

*
*   IF CONV CODE USED, FORCE CORRECT VALUE
*
         LOOP
            CRT TEXT:
            INPUT VALUE
            IF CONV.CODE THEN
               NEW.VALUE = ICONV(VALUE,CONV.CODE)
               ISTAT = STATUS()
            END ELSE
               NEW.VALUE = VALUE
               ISTAT = 0
            END
            IF ISTAT = 0 THEN
               CRT NEW.VALUE
            END
         UNTIL ISTAT = 0
            CRT '"':VALUE:'" DOES NOT MATCH THE CONV CODE "':CONV.CODE:'"'
         REPEAT
         VAR.VALUE(VAR.NUM) = NEW.VALUE
      END

*
*  RETURN VALUE OF VARIABLE
*
      RETURN.VALUE = VAR.VALUE(VAR.NUM)

      RETURN
   END
