      SUBROUTINE C26.ESCAPE(A.RETURN,A.IN)
*
* UNIBASIC SUBROUTINE
* 09/11/2002
* DLR
* TITLE ----- C26.ESCAPE
*
*
* MODULE :
* PURPOSE: REPLACE NON-ALPHANUMERIC CHARACTERS WITH ESCAPE SEQUENCES
*
* Stamped: p1 rotmand, /datatel/live/collive, user #25310, 11 Sep 02, 07:35AM.
*
*************************************************************************
*

      A.RETURN = ''
      LEN.A.IN = LEN(A.IN)
      IF LEN.A.IN = 0 THEN
         RETURN
      END

      X.ALLOWABLE = ''
* NUMERIC DIGITS
      FOR WHICH.CHAR = 48 TO 57
         X.ALLOWABLE := CHAR(WHICH.CHAR)
      NEXT WHICH.CHAR
* UPPERCASE ALPHA
      FOR WHICH.CHAR = 65 TO 90
         X.ALLOWABLE := CHAR(WHICH.CHAR)
      NEXT WHICH.CHAR
* LOWERCASE ALPHA
      FOR WHICH.CHAR = 97 TO 122
         X.ALLOWABLE := CHAR(WHICH.CHAR)
      NEXT WHICH.CHAR


      FOR WHICH.CHAR = 1 TO LEN.A.IN
         X.CHAR = A.IN[WHICH.CHAR,1]
         IF INDEX(X.ALLOWABLE,X.CHAR,1) GT 0 THEN
            A.RETURN := X.CHAR
         END ELSE
            X.HEX = ''
            X.DEC = SEQ(X.CHAR)
            CALL DEC.TO.HEX(X.HEX,X.DEC)
            A.RETURN := '%':FMT(X.HEX,'2"0"R')
         END
      NEXT WHICH.CHAR
      RETURN


   END
