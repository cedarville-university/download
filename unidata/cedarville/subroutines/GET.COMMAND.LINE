      SUBROUTINE GET.COMMAND.LINE(PROGRAM.NAME,OPTIONS,NUM.OPTIONS,OPT.VALUES,NUM.OPT.VALUES)
*
* INFO/BASIC SUBROUTINE
* 3/25/88
* DWS
* TITLE ----- GET.COMMAND.LINE
*
*
* PURPOSE: RETURN COMMAND LINE PRIME STYLE OPTIONS & VALUES
*            EG.  RUN BP PROGNAME -LIST ALL -CLEAR A B
*              RETURN VALUES:
*                PROGRAM:     PROGNAME
*                OPTIONS:  -LIST          -CLEAR
*                OPT.VALUES:     ALL            A
*                                            B
*
*
*     Last updated by LIVE (SJOQUIST) at 08:42:30 on 03/25/1988.
*
*************************************************************************
*
*  This program is provided 'as is' for your use by ROTMAN & SJOQUIST.
*  You are encouraged to verify the accuracy of program functions prior
*  to use with actual data files.
*
*  For a list of programs available for purchase from ROTMAN & SJOQUIST,
*  contact Dave Rotman or Doug Sjoquist:
*
*      Rotman and Sjoquist
*      c/o Cedarville College
*      PO Box 601
*      Cedarville, OH 45314
*      (513) 766-2211
*
*************************************************************************
*

      GOSUB SET.UP
      GOSUB FIND.PROGRAM.NAME
      GOSUB FIND.OPTIONS
      RETURN


*
*
*
*
SET.UP:
      COMMAND.LINE = @SENTENCE
      CALL PARSE.LINE(COMMAND.LINE,PARAM.STR,PARAM.QUOTED,NUM.PARAMS)
      PROGRAM.NAME = ''
      OPTIONS = ''
      NUM.OPTIONS = 0
      OPT.VALUES = ''
      NUM.OPT.VALUES = ''
      RETURN



*
*
* FIND THE PROGRAM NAME
*    CATALOGUED PROGRAMS HAVE PROGRAM NAME FIRST
*    NON CATALOGUED PROGRAMS HAVE IT THIRD (RUN BP PROGNAME)
*
FIND.PROGRAM.NAME:
      IF PARAM.STR<1> = 'RUN' THEN
         PARAM.CTR = 3
      END ELSE
         PARAM.CTR = 1
      END
      PROGRAM.NAME = PARAM.STR<PARAM.CTR>
      PARAM.CTR += 1
      RETURN


*
*
* FIND  OPTIONS AND VALUES
*
FIND.OPTIONS:
      LOOP
      UNTIL PARAM.CTR > NUM.PARAMS
         PARAM = PARAM.STR<PARAM.CTR>
         IF PARAM[1,1] = '-' AND NOT(PARAM.QUOTED<PARAM.CTR>) THEN
            NUM.OPTIONS += 1
            OPTIONS<NUM.OPTIONS> = PARAM
         END ELSE
*
*  SPECIAL CASE (NO -QUAL BEFORE THE FIRST OPTION)
*
            IF NUM.OPTIONS = 0 THEN
               NUM.OPTIONS = 1
               OPTIONS<NUM.OPTIONS> = ''
            END
            NUM.OPT.VALUES<NUM.OPTIONS> = NUM.OPT.VALUES<NUM.OPTIONS> + 1
            OPT.VALUES<NUM.OPTIONS,NUM.OPT.VALUES<NUM.OPTIONS>> = PARAM
         END
         PARAM.CTR += 1
      REPEAT
      RETURN

   END
