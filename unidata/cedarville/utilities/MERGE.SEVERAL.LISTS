*
* UNIBASIC PROGRAM
* 04/01/2011
* DLR
* TITLE ----- MERGE.SEVERAL.LISTS
*
* PURPOSE:  COMBINE SEVERAL SAVE LISTS WITHOUT HAVING TO MANIPULATE
*  SEVERAL EXECUTIONS OF MERGE.LIST
*
* Stamped: pts_10 rotmand, /datatel/coll18/live/apphome, user #3366, 01 Apr 11, 08:30AM.
*
*************************************************************************
*

      COMMAND.LINE = @SENTENCE
      GOSUB SET.UP
      NUM.XL.LIST.NAMES = DCOUNT(XL.LIST.NAMES,@VM)
      IF NUM.XL.LIST.NAMES LT 2 THEN
         GOSUB DISPLAY.HELP
         STOP
      END
      X.TEMP.LIST.NAME = @LOGNAME:'_MERGE.SEVERAL.LISTS'
      CRT XL.LIST.NAMES<1,1>
      EXECUTE 'GET.LIST ':XL.LIST.NAMES<1,1>
      EXECUTE 'SAVE.LIST ':X.TEMP.LIST.NAME
      FOR WHICH.XL.LIST.NAMES = 2 TO NUM.XL.LIST.NAMES
         CRT XL.LIST.NAMES<1,WHICH.XL.LIST.NAMES>
         EXECUTE 'GET.LIST ':X.TEMP.LIST.NAME:' TO 1'
         EXECUTE 'GET.LIST ':XL.LIST.NAMES<1,WHICH.XL.LIST.NAMES>:' TO 2'
         EXECUTE 'ML 1 ':X.LIST.OPER:' 2'
         EXECUTE 'SAVE.LIST ':X.TEMP.LIST.NAME
         X.LIST.NAMES = XL.LIST.NAMES<1,WHICH.XL.LIST.NAMES>
      NEXT WHICH.XL.LIST.NAMES
      EXECUTE 'GET.LIST ':X.TEMP.LIST.NAME
      STOP


SET.UP: 
      NUM.PARAMS = 0
      PARAM.QUOTED = ''
      PARAM.STR = ''
      CALL PARSE.LINE(COMMAND.LINE,PARAM.STR,PARAM.QUOTED,NUM.PARAMS)
      IF PARAM.STR<1> = 'RUN' THEN
         STARTING.PARAM = 4
      END ELSE
         STARTING.PARAM = 2
      END
      X.LIST.OPER = PARAM.STR<STARTING.PARAM>
      XL.LIST.NAMES = ''
      FOR WHICH.OPER = STARTING.PARAM+1 TO NUM.PARAMS
         XL.LIST.NAMES<1,-1> = PARAM.STR<WHICH.OPER>
      NEXT WHICH.OPER
      IF XL.LIST.NAMES = '' THEN
         GOSUB DISPLAY.HELP
         STOP
      END
      RETURN


DISPLAY.HELP: 
      CRT 'Syntax:'
      CRT 'MERGE.SEVERAL.LISTS ListOper ListName1 ListName2 ... ListNameN'
      CRT
      CRT 'ListOper can be one of:'
      CRT 'I      intersection'
      CRT 'INT    intersection'
      CRT 'UNION  union'
      CRT '+      union'
      CRT
      CRT 'Example:'
      CRT 'MERGE.SEVERAL.LISTS + CURRENT.2011S1 CURRENT.2011S2 CURRENT.2011S3 CURRENT.2011S4 CURRENT.2011S5'
      CRT 'SAVE.LIST CURRENT.ALL.SUMMER.UG'
      RETURN


   END
