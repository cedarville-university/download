*
*
* INFO/BASIC PROGRAM
* 07/01/93
* DLR
* TITLE ----- VANITY.PHONE
*
*
* MODULE :
* PURPOSE: TRANSLATE VANITY PHONE CHARACTERS TO NUMBERS
*
* Stamped: pts_70 rotmand, /datatel/live/collive, user #3565, 16 May 06, 11:26AM.
* Check for RUN in statement (ie using RUN LOCAL.SOURCE VANITY.PHONE)
*   versus using a cataloged verb (VANITY.PHONE).
* Stamped: pty/ttyp6 sjoquist, /disk1/collive, user #6096, 24 Jun 94, 05:04PM.
* Updated for HP-UX
*     Last updated by LIVE (ROTMAND) at 13:15:03 on 07/01/1993.
*
*************************************************************************
*

      COMMAND.LINE = @SENTENCE
      NUM.PARAMS = 0
      PARAM.QUOTED = ''
      PARAM.STR = ''
      CALL PARSE.LINE(COMMAND.LINE,PARAM.STR,PARAM.QUOTED,NUM.PARAMS)

      ALPHA.STRING = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
      NUMERIC.STRING = '2223334445556667Q77888999Z'

      IF PARAM.STR<2> = '' THEN
         PROMPT ''
         DONE = @FALSE
         LOOP
            PRINT 'Vanity number: ':
            INPUT VANITY.STRING
         UNTIL VANITY.STRING = ''
            VANITY.NUMBER = VANITY.STRING
            CONVERT ALPHA.STRING TO NUMERIC.STRING IN VANITY.NUMBER
            PRINT VANITY.STRING:' is ':VANITY.NUMBER:'.'
         REPEAT
      END ELSE
         IF PARAM.STR<1> = 'RUN' THEN
            STARTING.PARAM = 4
         END ELSE
            STARTING.PARAM = 2
         END
         FOR WHICH.PARAM = STARTING.PARAM TO NUM.PARAMS
            VANITY.STRING = PARAM.STR<WHICH.PARAM>
            VANITY.NUMBER = VANITY.STRING
            CONVERT ALPHA.STRING TO NUMERIC.STRING IN VANITY.NUMBER
            PRINT VANITY.STRING:' is ':VANITY.NUMBER:'.'
         NEXT WHICH.PARAM
      END
      STOP
   END
