*
* INFO/BASIC PROGRAM
* 3/13/90
* DWS
* TITLE ----- GET.ALTERNATE.STACKS
*
*
* PURPOSE: GET COMMAND "STACKS" FOR PROMPT.STACK (USED IN LOGIN PARAGRAPH)
*
*     Last updated by LIVE (SJOQUIST) at 08:26:43 on 03/13/1990.
*
*************************************************************************
*

$INCLUDE LOCAL.SOURCE I_PROMPT.STACK.COMMON

*
*
* MAIN CONTROL
*

      GOSUB SET.UP
      NUM.STACKS = COUNT(ALT.STACKWRITE.REC<2>,@VM) + (ALT.STACKWRITE.REC<2> # '')
      PRSTACK.PROG.NAMES = ''
      NUM.PRSTACK = 0
      FOR STACK.CTR=1 TO NUM.STACKS
         WHICH.PROG.NAME = ALT.STACKWRITE.REC<2,STACK.CTR>
         ALT.STATUS = OCONV(ALT.STACKWRITE.REC<3,STACK.CTR>,'MCU')
         GOSUB PROCESS.STACK
      NEXT STACK.CTR
      CURRENT.PROG.NAME = ''
      CURRENT.PROG.POS = ''
      NUM.PROG.STACK = ''
      PROG.STACK = ''
      STOP



*
*
* SET UP
*
SET.UP:
      ON.ERROR.RETURN = @FALSE
      ERRMSG = ''
      F.VOC = ''
      CALL OPEN.FILE(F.VOC, 'VOC', ON.ERROR.RETURN, ERRMSG)
      F.SAVEDLISTS = ''
      CALL OPEN.FILE(F.SAVEDLISTS, 'SAVEDLISTS', ON.ERROR.RETURN, ERRMSG)
      READ ALT.STACKWRITE.REC FROM F.VOC, 'ALTERNATE.STACKWRITE' ELSE
         ALT.STACKWRITE.REC = ''
      END
      RETURN


*
*
* PROCESS STACK
*
PROCESS.STACK:
      OKAY.TO.READ = @TRUE
      IF ALT.STATUS = 'OFF' THEN
         OKAY.TO.READ = @FALSE
      END
      IF OKAY.TO.READ THEN
         STACK.NAME = '&&':WHICH.PROG.NAME:'.':@LOGNAME:'.':@USERNO
         READ WHICH.PROG.STACK FROM F.SAVEDLISTS, STACK.NAME THEN
            CONVERT @FM TO @VM IN WHICH.PROG.STACK
            NUM.PRSTACK += 1
            PRSTACK.PROG.NAMES<NUM.PRSTACK> = WHICH.PROG.NAME
            PRSTACK.PROG.STACK<NUM.PRSTACK> = WHICH.PROG.STACK
            PRSTACK.NUM.PROG.STACK<NUM.PRSTACK> = COUNT(WHICH.PROG.STACK,@VM) + (WHICH.PROG.STACK # '')
         END
      END
      RETURN

   END
