      SUBROUTINE SORT.INDEX(MAT SORT.ARRAY,MAT INDEX.ARRAY,NUM.SORT,ORDER)
*
* INFO/BASIC SUBROUTINE
* 7/22/87
* DWS
* TITLE ----- SORT.INDEX
*
*
* PURPOSE:
*    Sort an index to an array using Shell sort algorithm
*      The index must be set up previous to the subroutine call
*      eg.
*         FOR I=1 TO NUM
*           INPUT NAME(I)
*           INPUT SCORE(I)
*           INDEX.ARRAY(I) = I
*         NEXT I
*         CALL SORT.INDEX(MAT NAME,MAT INDEX.ARRAY,NUM,'A')
*         FOR I=1 TO NUM
*          PRINT I,NAME(INDEX.ARRAY(I)),SCORE(INDEX.ARRAY(I))
*         NEXT I
*
*
* Stamped: pty/ttyp6 sjoquist, /disk1/collive, user #4292, 24 Jun 94, 03:33PM.
* Updated for HP-UX
* Stamped: pty/ttyp6 sjoquist, /disk1/collive, user #4292, 24 Jun 94, 03:34PM.
* Updated for HP-UX
*     Last updated by LIVE (SJOQUIST) at 11:24:08 on 04/14/1988.
* SPLIT INTO 2 SUBROUTINES FOR PERFORMANCE
*     Last updated by LIVE (SJOQUIST) at 13:32:59 on 07/23/1987.
*
********************************************************************
*
*
*
*************************************************************************
*
*  This program is provided 'as is' for your use by ROTMAN & SJOQUIST.
*  You are encouraged to verify the accuracy of program functions prior
*  to use with actual data files.
*
*  If you have questions about this program, or if you wish to obtain
*  a list of programs available for purchase from ROTMAN & SJOQUIST,
*  contact Dave Rotman or Doug Sjoquist:
*
*      Rotman and Sjoquist
*      c/o Cedarville College
*      PO Box 601
*      Cedarville, OH 45314
*      (513) 766-2211
*
*************************************************************************
*
*


      DIM SORT.ARRAY(NUM.SORT),INDEX.ARRAY(NUM.SORT)

*
*
* SEPARATE SUBROUTINES FOR ASC & DESC (FOR PERFORMANCE)
*
      IF ORDER[1,1]='D' THEN
         GOSUB SORT.DESCENDING
      END ELSE
         GOSUB SORT.ASCENDING
      END
      RETURN

*
*
* ASCENDING
*
SORT.ASCENDING: 
      H = 1
      LOOP
         H = H*3 + 1
      UNTIL H > NUM.SORT
      REPEAT

      LOOP
         H = INT(H/3)
         FOR I=H+1 TO NUM.SORT
            V = SORT.ARRAY(INDEX.ARRAY(I))
            VINDEX.ARRAY = INDEX.ARRAY(I)
            J = I
            LOOP
               ITEM.A = SORT.ARRAY(INDEX.ARRAY(J-H))
               ITEM.B = V
            UNTIL ITEM.A < ITEM.B
               INDEX.ARRAY(J) = INDEX.ARRAY(J-H)
               J = J - H
               IF J <= H THEN GOTO DONE.ASC
            REPEAT
DONE.ASC:   
            INDEX.ARRAY(J) = VINDEX.ARRAY
         NEXT I
      UNTIL H = 1
      REPEAT

      RETURN

*
*
* DESCENDING
*
SORT.DESCENDING: 
      H = 1
      LOOP
         H = H*3 + 1
      UNTIL H > NUM.SORT
      REPEAT

      LOOP
         H = INT(H/3)
         FOR I=H+1 TO NUM.SORT
            V = SORT.ARRAY(INDEX.ARRAY(I))
            VINDEX.ARRAY = INDEX.ARRAY(I)
            J = I
            LOOP
               ITEM.A = V
               ITEM.B = SORT.ARRAY(INDEX.ARRAY(J-H))
            UNTIL ITEM.A < ITEM.B
               INDEX.ARRAY(J) = INDEX.ARRAY(J-H)
               J = J - H
               IF J <= H THEN GOTO DONE.DES
            REPEAT
DONE.DES:   
            INDEX.ARRAY(J) = VINDEX.ARRAY
         NEXT I
      UNTIL H = 1
      REPEAT

      RETURN
   END
