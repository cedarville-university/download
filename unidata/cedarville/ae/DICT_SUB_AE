      SUBROUTINE DICT_SUB_AE(MAT X$COMS,PASSED.COMMAND,NUM.LINES.TO.SHOW,COMMAND.OPTIONS)
*
* INFO/BASIC SUBROUTINE
* 05/09/97
* DLR (DAVE ROTMAN)
* TITLE ----- DICT_SUB_AE
*
*
* MODULE :
* PURPOSE: DISPLAY DICTIONARY FIELD NAME WHILE EDITING A RECORD
*    CALLED BY $DICT AND $CONV
*
* Stamped: p1 rotmand, /disk1/collive, user #11731, 12 May 97, 07:50AM.
* Stamped: pb rotmand, /disk1/collive, user #5248, 09 May 97, 02:09PM.
*
*************************************************************************
*

      $INSERT I_AE_XCOMS FROM AE_XCOMS


      COMMON /DICTAE/ DICTAE.FILE.NAME,
      DICTAE.DICT.NAME.LIST,
      DICTAE.DICT.LOC.LIST,
      DICTAE.DICT.CONV.LIST,
      DICTAE.SAVE.NUM.LINES.TO.SHOW,
      DICTAE.DICT.FMT

      IF X$FN # DICTAE.FILE.NAME THEN
         GOSUB READ.DICT
      END

      BEGIN CASE
         CASE PASSED.COMMAND = 'DICT' OR PASSED.COMMAND = 'CONV'
            DICTAE.SAVE.NUM.LINES.TO.SHOW = 1
            START.POSITION = X$PO
            END.POSITION = X$PO
         CASE NUM.LINES.TO.SHOW = 'USE.PRIOR'
            IF DICTAE.SAVE.NUM.LINES.TO.SHOW THEN
               NULL
            END ELSE
               DICTAE.SAVE.NUM.LINES.TO.SHOW = 20
            END
            START.POSITION = X$PO + 1
            END.POSITION = X$PO + DICTAE.SAVE.NUM.LINES.TO.SHOW
         CASE NUM(NUM.LINES.TO.SHOW) AND NUM.LINES.TO.SHOW GT 0
            DICTAE.SAVE.NUM.LINES.TO.SHOW = NUM.LINES.TO.SHOW
            START.POSITION = X$PO + 1
            END.POSITION = X$PO + DICTAE.SAVE.NUM.LINES.TO.SHOW
         CASE 1
            DICTAE.SAVE.NUM.LINES.TO.SHOW = 20
            START.POSITION = X$PO + 1
            END.POSITION = X$PO + DICTAE.SAVE.NUM.LINES.TO.SHOW
      END CASE
      IF START.POSITION GT X$B.CT THEN
         START.POSITION = X$B.CT
      END
      IF END.POSITION GT X$B.CT THEN
         END.POSITION = X$B.CT
      END

      UC.COMMAND.OPTIONS = OCONV(COMMAND.OPTIONS,'MCU')

      IF X$B.CT < 100 THEN
         FIELD.NUM.LEN = 3
      END ELSE
         FIELD.NUM.LEN = LEN(X$B.CT)
      END
      FIELD.NUM.FMT = FIELD.NUM.LEN:'"0"R'
      FIELD.MARGIN = FMT(' ',DICTAE.DICT.FMT):SPACE(FIELD.NUM.LEN+2)
      TERMINAL.WIDTH = @CRTWIDE - 1
      VALUE.LEN = TERMINAL.WIDTH - LEN(FIELD.MARGIN) - 1


      IF END.POSITION = 0 THEN
         CRT 'You are at line zero, so there is no dictionary information.'
         X$PO = END.POSITION
         RETURN
      END
      FOR WHICH.LINE = START.POSITION TO END.POSITION
         FORMATTED.DICT.LOC = FMT(WHICH.LINE,'5"0"R')
         LOCATE FORMATTED.DICT.LOC IN DICTAE.DICT.LOC.LIST<1,1> SETTING DICT.POS THEN
            DICT.NAME = DICTAE.DICT.NAME.LIST<1,DICT.POS>
         END ELSE
            DICT.NAME = '(line ':WHICH.LINE:')'
         END
         GOSUB FORMAT.OUTPUT
      NEXT WHICH.LINE
      X$PO = END.POSITION
      RETURN



FORMAT.OUTPUT: 
      PRINT.LINE = FMT(DICT.NAME,DICTAE.DICT.FMT)
      PRINT.LINE := ' ':FMT(WHICH.LINE,FIELD.NUM.FMT):':'
      LOCATE 'CONV' IN UC.COMMAND.OPTIONS<1,1> SETTING OPTION.POS THEN
         GOSUB DO.CONV
      END ELSE
         TEMP.OUT = X$REC<WHICH.LINE>
      END
      GOSUB DISPLAY.RESULTS
      RETURN


DO.CONV: 
      DICT.CONV = DICTAE.DICT.CONV.LIST<1,DICT.POS>
      TEMP.FIELD = X$REC<WHICH.LINE>
      NUM.VALUES = DCOUNT(TEMP.FIELD,@VM)
      TEMP.OUT = ''
      FOR WHICH.VALUE = 1 TO NUM.VALUES
         TEMP.VALUE = TEMP.FIELD<1,WHICH.VALUE>
         NUM.SUBVALUES = DCOUNT(TEMP.VALUE,@SM)
         FOR WHICH.SUBVALUE = 1 TO NUM.SUBVALUES
            TEMP.SUBVALUE = TEMP.VALUE<1,1,WHICH.SUBVALUE>
            TEMP.OUT<1,WHICH.VALUE,WHICH.SUBVALUE> =OCONV(TEMP.SUBVALUE,DICT.CONV)
         NEXT WHICH.SUBVALUE
      NEXT WHICH.VALUE
      RETURN


DISPLAY.RESULTS: 
      NUM.STRINGS = INT((LEN(TEMP.OUT)+VALUE.LEN-1)/VALUE.LEN)
      IF NUM.STRINGS LT 1 THEN
         CRT PRINT.LINE
      END ELSE
         FOR WHICH.STRING = 1 TO NUM.STRINGS
            ISTART = 1 + (WHICH.STRING-1) * VALUE.LEN
            PRINT.LINE := ' ':TEMP.OUT[ISTART,VALUE.LEN]
            CRT PRINT.LINE
            PRINT.LINE = FIELD.MARGIN
         NEXT WHICH.STRING
      END
      RETURN


READ.DICT: 
      FILE.NAME = X$FN
      OPEN 'DICT',FILE.NAME TO D.FILE.NAME ELSE
         CRT 'Unable to open DICT ':FILE.NAME
         RETURN
      END
      DICT.LIST.NUMBER = ''
      CALL GET.UNUSED.SELECT.LIST(DICT.LIST.NUMBER)
      SELECT D.FILE.NAME TO DICT.LIST.NUMBER
      READLIST TEMP.DICT.NAME.LIST FROM DICT.LIST.NUMBER ELSE
         TEMP.DICT.NAME.LIST = ''
      END
      DICTAE.DICT.NAME.LIST = ''
      DICTAE.DICT.LOC.LIST = ''
      NUM.DICTAE = 0
      CONVERT @IM:@FM TO @VM:@VM IN TEMP.DICT.NAME.LIST
      NUM.DICT.NAME.LIST = DCOUNT(TEMP.DICT.NAME.LIST,@VM)
      MAX.DICT.LEN = 1
      FOR WHICH.DICT = 1 TO NUM.DICT.NAME.LIST
         DICT.NAME = TEMP.DICT.NAME.LIST<1,WHICH.DICT>
         READ DICT.REC FROM D.FILE.NAME, DICT.NAME THEN
            IF DICT.REC<1>[1,1] = 'D' THEN
               DICT.LOC = DICT.REC<2>
               DICT.CONV = DICT.REC<3>
               NUM.DICTAE += 1
               DICTAE.DICT.NAME.LIST<1,NUM.DICTAE> = DICT.NAME
               DICTAE.DICT.LOC.LIST<1,NUM.DICTAE> = FMT(DICT.LOC,'5"0"R')
               DICTAE.DICT.CONV.LIST<1,NUM.DICTAE> = DICT.CONV
            END
         END
         IF LEN(DICT.NAME) GT MAX.DICT.LEN THEN
            MAX.DICT.LEN = LEN(DICT.NAME)
         END
      NEXT WHICH.DICT
      DICTAE.DICT.FMT = MAX.DICT.LEN:'R'
      DICTAE.FILE.NAME = FILE.NAME
      RETURN


   END
