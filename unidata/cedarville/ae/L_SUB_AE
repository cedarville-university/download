      SUBROUTINE L_SUB_AE(MAT X$COMS,PASSED.COMMAND)
*
* INFO/BASIC SUBROUTINE
* 05/09/97
* DLR (DAVE ROTMAN)/SJH
* TITLE ----- L_SUB_AE
*
*
* MODULE :
* PURPOSE:  LOCATE THE NEXT x OCCURRENCES OF A STRING
*
* Stamped: pty/ttysf howders, /disk1/collive, user #10743, 09 May 97, 02:40PM.
*
*************************************************************************
*

      $INSERT I_AE_XCOMS FROM AE_XCOMS

      NUMBER.TO.LOCATE = FIELD(PASSED.COMMAND,' ',2,1)
      IF NUMBER.TO.LOCATE = '' OR NOT(NUM(NUMBER.TO.LOCATE)) THEN
         GOSUB DISPLAY.HELP
         RETURN
      END
      TEXT.TO.LOCATE = FIELD(PASSED.COMMAND,' ',3,99)
      MAX.LINES = X$B.CT
      IF MAX.LINES < 1000 THEN
         LINE.FMT = '3"0"R'
      END ELSE
         LINE.FMT = LEN(MAX.LINES):'"0"R'
      END
      NUMBER.MATCHES = 0
      IF NUMBER.TO.LOCATE GT 0 THEN
         GOSUB SEARCH.FORWARD
      END ELSE
         GOSUB SEARCH.BACKWARD
      END
      RETURN


SEARCH.FORWARD: 
      LOOP
      UNTIL NUMBER.MATCHES = NUMBER.TO.LOCATE OR X$PO = MAX.LINES
         X$PO += 1
         IF INDEX(X$REC<X$PO>,TEXT.TO.LOCATE,1) > 0 THEN
            PRINT.LINE = FMT(X$PO,LINE.FMT):':'
            PRINT.LINE := ' ':X$REC<X$PO>
            CRT PRINT.LINE
            NUMBER.MATCHES += 1
         END
      REPEAT
      IF X$PO = MAX.LINES THEN
         CRT 'End of file'
      END
      RETURN


SEARCH.BACKWARD: 
      LOOP
      UNTIL NUMBER.MATCHES = NUMBER.TO.LOCATE OR X$PO = 0
         X$PO -= 1
         IF INDEX(X$REC<X$PO>,TEXT.TO.LOCATE,1) > 0 THEN
            PRINT.LINE = FMT(X$PO,LINE.FMT):':'
            PRINT.LINE := ' ':X$REC<X$PO>
            CRT PRINT.LINE
            NUMBER.MATCHES -= 1
         END
      REPEAT
      IF X$PO = 0 THEN
         CRT 'Top of file'
      END
      RETURN


DISPLAY.HELP: 
      CRT 'You must specify the number of matches to find and the text to search for.'
      RETURN


   END
