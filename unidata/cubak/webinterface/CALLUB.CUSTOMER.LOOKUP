      SUBROUTINE CALLUB.CUSTOMER.LOOKUP(RETURN.VALUE,PASSED.ARGS)
*
* UNIBASIC SUBROUTINE
* 11/19/2001
* DAVE ROTMAN
* TITLE ----- CALLUB.CUSTOMER.LOOKUP
*
*
* MODULE :
* PURPOSE: SAMPLE PROGRAM TO ILLUSTRATE RETURNING WEB PAGES VIA RSH
*
* Stamped: p1 rotmand, /datatel/live/collive, user #10466, 19 Nov 01, 02:42PM.

      CALLUB.BR = '<BR>'

      RETURN.VALUE = 0
*
* We call a subroutine to start each Web page.  You could do that, or
* you could hard code the HTML tags here (<HTML>,<HEAD>,<BODY>, etc.).
*
* X.TITLE.TEXT     text to include as HTML title (top bar of browser)
* X.INCLUDE.IMAGE  'Y' to include graphic and image map
* X.CACHE          'Y' to turn on caching ('N' for no-cache)
      X.TITLE.TEXT = 'Customer Lookup'
      X.INCLUDE.IMAGE = 'Y'
      X.CACHE = 'N'
      CALL CALLUB.MANAGE.HTML('TOP',X.TITLE.TEXT,X.INCLUDE.IMAGE,X.CACHE)

      CALLUB.HTML.STARTED = @TRUE

      CALLUB.HTML.TITLE = '<h3>'
      CALLUB.HTML.TITLE := 'Customer Lookup as of ':OCONV(DATE(),'D4/'):' ':OCONV(TIME(),'MTH')
      CALLUB.HTML.TITLE := '</h3>'
      PRINT CALLUB.HTML.TITLE
      GOSUB SET.UP
      CUSTOMER.ID.NO = PASSED.ARGS<4>
      GOSUB PROCESS.REQUEST
*
* Finish off the HTML code.
      CALL CALLUB.MANAGE.HTML('BOTTOM',X.TITLE.TEXT,X.INCLUDE.IMAGE,X.CACHE)
*
* Keep track of the activity.
      CALL LOG.CALLUB.REQUEST(ERROR.MSG)
      RETURN


SET.UP: 
      ERROR.MSG = ''
      PROGRAM.TEXT = 'Customer Lookup'
      DATA.FILE.NAME = 'CUSTOMERS'
      OPEN '',DATA.FILE.NAME TO F.DATA.FILE ELSE
         ERROR.NUMBER = 100
         ERROR.MSG = 'Unable to access data on host, error number ':ERROR.NUMBER
         CALL LOG.CALLUB.REQUEST(ERROR.MSG)
         CALL CALLUB.ERROR(PROGRAM.TEXT,ERROR.MSG)
         STOP
      END
      RETURN



PROCESS.REQUEST: 
* Read necessary files, format output, etc.
* For this demo version, we just echo the arguments to the screen.
      PRINT 'Processing customer ':CUSTOMER.ID.NO:CALLUB.BR
      PRINT 'HERE ARE THE ITEMS FOUND IN THE QUERY_STRING:':CALLUB.BR
      CALLUB.NUM.ITEMS = DCOUNT(PASSED.ARGS,@FM)
      FOR WHICH.ITEM = 1 TO CALLUB.NUM.ITEMS
         PRINT WHICH.ITEM:' ':PASSED.ARGS<WHICH.ITEM>:CALLUB.BR
      NEXT WHICH.ITEM
* Now, finish the HTML document.
      PRINT.LINE = 'Contact '
      PRINT.LINE := '<A HREF="mailto:contact@yoursite.com">'
      PRINT.LINE := 'contactname</A> in the event of an error.':CALLUB.BR
      PRINT PRINT.LINE
      PRINT CALLUB.BR

      RETURN




   END
