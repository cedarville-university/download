*
* INFO/BASIC PROGRAM
* 2/11/88
* DWS
* TITLE ----- PRINT.PAGE
*
*
* MODULE:  PRINT.PAGE
* PURPOSE: LOAD PARAMETERS FROM FILE FOR PRINT.PAGE ROUTINES
*
* Stamped: qc rotmand, /datatel/live/collive, user #8284, 18 Dec 00, 09:20AM.
*  Version 4.0
*  Add FONT predefined field.
*     Last updated by LIVE (ROTMAN) at 09:36:40 on 09/11/1989.
* Allow SUBROUTINE field type.
* Add &INITIALIZATION and &TERMINATION sections to definition file.
* Shorten subroutine names for customers with long account names.
*     Last updated by LIVE (SJOQUIST) at 09:04:49 on 02/07/1989.
*     Last updated by LIVE (SJOQUIST) at 15:55:51 on 12/14/1988.
* Update to 3.0
*     Last updated by LIVE (SJOQUIST) at 14:42:39 on 02/12/1988.
*        Allow multiple pages per run
*     Last updated by LIVE (SJOQUIST) at 13:48:15 on 02/11/1988.
*        Split into control program, Init sub, & Process sub
*     Last updated by LIVE (SJOQUIST) at 09:40:01 on 01/14/1988.
*
*
*************************************************************************
*
*  COPYRIGHT (C) 1988, 1989, ROTMAN & SJOQUIST
*
*      The information contained in this file is proprietary to
*      ROTMAN & SJOQUIST and shall not be reproduced in part or
*      in whole without their prior written authorization.
*      This file may be modified for the internal use of this
*      institution, but no part of this file nor any program or
*      file derived from it may be distributed to any other
*      individual or institution.
*
*************************************************************************
*
      $INSERT I_PRINT.PAGE.COMMON
*1

*
*
* MAIN CONTROL
*
      GOSUB SET.UP
      IF ERRMSG THEN
         ERRMSG<-1> = '** No processing done **'
         GOSUB PRINT.ERRORS
         STOP
      END
      CALL PRINT.PAGE.INIT
      FORM.NUM = 1
      CALL PRINT.PAGE.LOAD(WP.DIR,WP.FILE,INFO.FILE,FORM.NUM,ERRMSG)
      IF ERRMSG THEN
         ERRMSG<-1> = '** Can not continue with errors in loading page **'
         GOSUB PRINT.ERRORS
         STOP
      END
      IF PP.HAVE.ATTACH THEN
         FORM.NUM = 2
         CALL PRINT.PAGE.LOAD(WP.DIR,PP.ATTACH.FORM.NAME,INFO.FILE,FORM.NUM,ERRMSG)
         IF ERRMSG THEN
            ERRMSG<-1> = '** Can not continue with errors in loading page **'
            GOSUB PRINT.ERRORS
            STOP
         END
      END
      IF PP.INITIALIZATION THEN
         EXECUTE PP.INITIALIZATION
      END
      GOSUB PROCESS.PAGE
      IF ERRMSG THEN
         IF DISPLAY.ERRORS THEN
            GOSUB PRINT.ERRORS
         END
      END ELSE
         IF PP.TERMINATION THEN
            EXECUTE PP.TERMINATION
         END
      END
      GOSUB TERMINATE
      STOP


*
* SET UP
*
SET.UP: 
      NUM.ACTUAL.LINES = ''
      ERRMSG = ''
      FORM.NUM = 1
      PROMPT ''
      COMMAND.LINE = @SENTENCE
      PARAM.STR = ''
      PARAM.QUOTED = ''
      NUM.PARAMS = 0
      CALL PP.PARSE.LINE(COMMAND.LINE,PARAM.STR,PARAM.QUOTED,NUM.PARAMS)
      IF PARAM.STR<1> = 'RUN' THEN
         STARTING.PARAM = 4
      END ELSE
         STARTING.PARAM = 2
      END

      LOCATE 'HELP' IN PARAM.STR<1> SETTING POS THEN
         GOSUB DISPLAY.HELP
         STOP
      END
      LOCATE '-HELP' IN PARAM.STR<1> SETTING POS THEN
         GOSUB DISPLAY.HELP
         STOP
      END
      IF NUM.PARAMS < STARTING.PARAM + 2 THEN
         ERRMSG<-1> = 'The proper syntax Print.Page 4.0 is:'
         ERRMSG<-1> = '  PRINT.PAGE data.file wp.directory wp.file [-FORM form.name] [-COPIES #]'
         ERRMSG<-1> = '  '
         ERRMSG<-1> = '    where wp.directory is the type 1 file (directory) containing'
         ERRMSG<-1> = '    the form definition files and wp.file is the form (record) to be printed.'
         ERRMSG<-1> = '  '
         ERRMSG<-1> = 'Type PRINT.PAGE HELP for more information.'
         ERRMSG<-1> = '  '
         RETURN
      END
      INFO.FILE = PARAM.STR<STARTING.PARAM>
      WP.DIR = PARAM.STR<STARTING.PARAM + 1>
      WP.FILE = PARAM.STR<STARTING.PARAM + 2>
      LOCATE '-FORM' IN PARAM.STR<1> SETTING FORM.POS THEN
         FORM.NAME = PARAM.STR<FORM.POS+1>
      END ELSE
         FORM.NAME = PARAM.STR<5>
      END
      LOCATE '-COPIES' IN PARAM.STR<1> SETTING COPIES.POS THEN
         COPIES = PARAM.STR<COPIES.POS+1>
      END ELSE
         COPIES = ''
      END
      LOCATE '-AT' IN PARAM.STR<1> SETTING AT.POS THEN
         AT.NAME = PARAM.STR<AT.POS+1>
      END ELSE
         AT.NAME = ''
      END
      LOCATE '-TTY' IN PARAM.STR<1> SETTING POS THEN
         FORM.NAME = ''
         USE.PRINTER = 0
      END ELSE
         USE.PRINTER = 1
      END
      LOCATE '-NODISPLAY' IN PARAM.STR<1> SETTING POS THEN
         DISPLAY.COUNT = 0
      END ELSE
         DISPLAY.COUNT = 1
      END
      LOCATE '-NOERRORS' IN PARAM.STR<1> SETTING POS THEN
         DISPLAY.ERRORS = 0
      END ELSE
         DISPLAY.ERRORS = 1
      END
*
      MAIL.ID.FIELD = ''
      MAIL.ID.FIELD.NUM = ''
      PP.ATTACH.FORM.NAME = ''
      PP.ATTACH.TEXT = ''
      MAIL.F.NETWORK.LOGINS = ''
      MAIL.SUBJECT = ''
      PP.HAVE.MAIL = @FALSE
      PP.HAVE.ATTACH = @FALSE
      LOCATE '-MAIL' IN PARAM.STR<1> SETTING MAIL.POS THEN
         PP.HAVE.MAIL = @TRUE
         MAIL.ID.FIELD = PARAM.STR<MAIL.POS+1>
      END
      IF PP.HAVE.MAIL THEN
         LOCATE '-ATTACH' IN PARAM.STR<1> SETTING ATTACH.POS THEN
            PP.HAVE.ATTACH = @TRUE
            PP.ATTACH.FORM.NAME = PARAM.STR<ATTACH.POS+1>
         END
         LOCATE '-SUBJECT' IN PARAM.STR<1> SETTING SUBJECT.POS THEN
            MAIL.SUBJECT = PARAM.STR<SUBJECT.POS+1>
         END ELSE
            MAIL.SUBJECT = WP.DIR:' ':WP.FILE
         END
         LOCATE '-SENDER' IN PARAM.STR<1> SETTING SENDER.POS THEN
            MAIL.SENDER = PARAM.STR<SENDER.POS+1>
         END ELSE
            MAIL.SENDER = @LOGNAME
         END
         USE.PRINTER = @FALSE
      END
*
* MAKE SURE FILES EXIST
*
*3
*
      OPEN '',INFO.FILE TO F.INFO ELSE
         ERRMSG<-1> = 'Can not open data file "':INFO.FILE:'"'
      END
      OPEN '', WP.DIR TO F.TEMP THEN
         READV TEST FROM F.TEMP, WP.FILE, 0 ELSE
            ERRMSG<-1> = 'Can not find "':WP.FILE:'" in "':WP.DIR:'"'
         END
         CLOSE F.TEMP
      END ELSE
         ERRMSG<-1> = 'Can not open type-1 file "':WP.DIR:'"'
      END

      TRUE = 1
      FALSE = 0
      NUM.SELECTED.PRIOR.TO.INIT = @SYSTEM.RETURN.CODE
*** SETPTR CALL
      IF USE.PRINTER THEN
         SETPTR.COMMAND = 'SETPTR ,,,0,0,,NFMT,NHEAD,BRIEF'
         IF FORM.NAME THEN
            SETPTR.COMMAND := ',FORM ':FORM.NAME
         END
         IF COPIES THEN
            SETPTR.COMMAND := ',COPIES ':COPIES
         END
         IF AT.NAME THEN
            SETPTR.COMMAND := ',AT ':AT.NAME
         END
         EXECUTE SETPTR.COMMAND
      END
      RETURN


*
*
* TERMINATE
*
TERMINATE: 
      IF FORM.NAME OR AT.NAME THEN
         EXECUTE 'SETPTR ,,,,,,BRIEF'
      END
      IF USE.PRINTER THEN
         PRINTER CLOSE
      END
      RETURN



*
*
* PROCESS PAGE
*
PROCESS.PAGE: 
      NUM.SELECTED.AFTER.INIT = @SYSTEM.RETURN.CODE
      IF NUM.SELECTED.AFTER.INIT > NUM.SELECTED.PRIOR.TO.INIT THEN
         NUM.SELECTED = NUM.SELECTED.AFTER.INIT
      END ELSE
         NUM.SELECTED = NUM.SELECTED.PRIOR.TO.INIT
      END
      IF DISPLAY.COUNT THEN
         GOSUB DISPLAY.HEADING
      END
      IF USE.PRINTER THEN
         PRINTER ON
      END
      IF USE.PRINTER THEN
         NO.DISPLAY = @(0,0)
      END
      DONE = FALSE
      LOOP
         READNEXT @ID ELSE DONE = TRUE
      UNTIL DONE
         INFO.KEY = @ID<1,1>
         INFO.VALUE.NUM = @ID<1,2>
         INFO.SUBVALE.NUM = @ID<1,3>
         INFO.FOUND = TRUE
         READ @RECORD FROM F.INFO,INFO.KEY ELSE
            INFO.FOUND = FALSE
         END
         IF INFO.FOUND THEN
            IF USE.PRINTER THEN
               IF DISPLAY.COUNT THEN
                  CRT CRT.POS:FMT(NUM.RECS(FORM.NUM)+1,'5R')
               END
            END ELSE
               HEADING '-- RECORD KEY = "':INFO.KEY:'"        COUNT = ':NUM.RECS(FORM.NUM)+1
            END
            IF PP.HAVE.ATTACH THEN
               FORM.NUM = 2
               CALL PRINT.PAGE.PROCESS(FORM.NUM,NUM.ACTUAL.LINES,ERRMSG)
            END
            FORM.NUM = 1
            CALL PRINT.PAGE.PROCESS(FORM.NUM,NUM.ACTUAL.LINES,ERRMSG)
         END ELSE
            ERRMSG<-1> = 'Record "':INFO.KEY:'" not found'
            IF DISPLAY.COUNT THEN
               CRT
               CRT 'Record "':INFO.KEY:'" not found'
            END
         END
      REPEAT
      IF USE.PRINTER THEN
         PRINTER OFF
      END
      RETURN

*
* DISPLAY HEADING
*
DISPLAY.HEADING: 
      IF USE.PRINTER THEN
         CRT @(-1)
      END
      CRT
      CRT 'PRINT.PAGE Version 4.0'
      CRT 'Copyright 1989-2000, Rotman & Howder'
      CRT
      CRT 'PAGE........: "WP" "':WP.FILE:'"'
      CRT 'FILE........: "':INFO.FILE:'"'
      CRT 'NUM.SELECTED: ':NUM.SELECTED
      CRT 'NUM.FIELDS..: ':NUM.FIELDS(FORM.NUM)
      IF USE.PRINTER THEN
         CRT @(1,15):'RECORD BEING PROCESSED: ':
         CRT.POS = @(25,15)
      END
      RETURN





*
*
* PRINT ERRORS
*
PRINT.ERRORS: 
      HEADING "'C'PRINT.PAGE Error Listing'LL'"
      DELIM = 999
      LOOP
      UNTIL DELIM = 0
         MSG = REMOVE(ERRMSG,DELIM)
         PRINT MSG
      REPEAT
      RETURN


DISPLAY.HELP: 
      OPEN '','VOC' TO F.VOC ELSE
         CRT 'Cannot open VOC file.'
         STOP
      END
      READ VOC.HELP.REC FROM F.VOC, 'PRINT.PAGE.HELP' THEN
         VOC.TYPE = FIELD(VOC.HELP.REC<1>,' ',1,1)
         IF VOC.TYPE = 'S' OR VOC.TYPE = 'PA' OR VOC.TYPE = 'C' THEN
            EXECUTE 'PRINT.PAGE.HELP'
            STOP
         END
      END
      CRT
      CRT 'PRINT.PAGE.HELP is not available.  Contact your system administrator.'
      RETURN

   END
