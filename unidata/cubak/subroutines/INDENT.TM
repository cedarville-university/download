      SUBROUTINE INDENT.TM(RETURN.FIELD, SOURCE.FIELD, STRLEN, INDENT)
*
* INFO/BASIC SUBROUTINE
* 1/24/90
* DWS
* TITLE ----- INDENT.TM
*
*
* MODULE :
* PURPOSE: DO A FMT(SOURCE.FIELD, 'nnT') WITH INDENTS ON LINES 2-? FOR EACH VALUE
*
* Stamped: pty/ttyp8 sjoquist, /disk1/collive, user #12854, 24 Jun 94, 08:28PM.
* Updated for HP-UX
*     Last updated by LIVE (SJOQUIST) at 12:25:11 on 01/24/1990.
*
*************************************************************************
*

      NUM.VALUES = COUNT(SOURCE.FIELD,@VM) + 1
      RETURN.FIELD = ''
      FOR VALUE.CTR=1 TO NUM.VALUES
         VALUE = SOURCE.FIELD<1,VALUE.CTR>
         NUM.SUBVALUES = COUNT(VALUE,@SM) + 1
         FOR SUBVALUE.CTR=1 TO NUM.SUBVALUES
            RETURN.SUBVALUE = ''
            SUBVALUE = VALUE<1,1,SUBVALUE.CTR>
            FMT.SUBVALUE = FMT(SUBVALUE, STRLEN:'T')
            NUM.TM = COUNT(FMT.SUBVALUE, @TM) + 1
            LEN.FIRST = LEN(FIELD(FMT.SUBVALUE,@TM,1,1))
            RETURN.SUBVALUE = TRIMB(FIELD(FMT.SUBVALUE,@TM,1,1))
            IF NUM.TM > 1 THEN
               REST.OF.LINE = SUBVALUE[LEN.FIRST+1,32000]
               FMT.SUBVALUE = FMT(REST.OF.LINE,(STRLEN-INDENT):'T')
               NUM.TM = COUNT(FMT.SUBVALUE, @TM) + 1
               FOR TM.CTR=1 TO NUM.TM
                  RETURN.SUBVALUE := @TM:SPACE(INDENT):TRIMB(TRIMF(FIELD(FMT.SUBVALUE,@TM,TM.CTR,1)))
               NEXT TM.CTR
            END
            RETURN.FIELD<1,VALUE.CTR,SUBVALUE.CTR> = RETURN.SUBVALUE
         NEXT SUBVALUE.CTR
      NEXT VALUE.CTR
      RETURN
   END
