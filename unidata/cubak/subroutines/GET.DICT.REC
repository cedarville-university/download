      SUBROUTINE GET.DICT.REC(FILE.NAME, FIELD.NAME, QUIET.MODE, DICT.REC, ERROR.MESSAGE)
*
* INFO/BASIC SUBROUTINE
* 07/15/94
* DWS
* TITLE ----- GET.DICT.REC
*
*
* PURPOSE: LOAD DICTIONARY RECORD, COMPILE I-DESC IT IF NECESSARY
*
* Stamped: pty/ttys3 sjoquist, /disk1/collive, user #17859, 15 Jul 94, 09:58AM.
*
*************************************************************************
*

      COMMON /GETIDES/ GETIDES.FILE.NAME,
      GETIDES.F.DICT.FILE,
      GETIDES.SPARE1,
      GETIDES.SPARE2,
      GETIDES.SPARE3,
      GETIDES.SPARE4,
      GETIDES.SPARE5

      GOSUB SET.UP
      IF GETIDES.FILE.NAME # FILE.NAME THEN
         GOSUB OPEN.FILE
         IF ERROR.MESSAGE THEN RETURN
      END
      GOSUB READ.DICT
      IF ERROR.MESSAGE THEN RETURN
      IF FIELD.TYPE = 'I' THEN
         GOSUB CHECK.COMPILE
      END
      IF ERROR.MESSAGE THEN RETURN
      RETURN



SET.UP: 
      ERROR.MESSAGE = ''
      DICT.REC = ''
      RETURN


OPEN.FILE: 
      OPEN 'DICT', FILE.NAME TO GETIDES.F.DICT.FILE THEN
         GETIDESC.FILE.NAME = FILE.NAME
      END ELSE
         ERROR.MESSAGE = 'Can not open "DICT" "':FILE.NAME:'"'
         GOSUB HANDLE.ERROR
         RETURN
      END
      RETURN


READ.DICT: 
      READ DICT.REC FROM GETIDES.F.DICT.FILE, FIELD.NAME ELSE
         ERROR.MESSAGE = 'Can not read "':FIELD.NAME:'" from "DICT" "':FILE.NAME:'"'
         GOSUB HANDLE.ERROR
         RETURN
      END
      FIELD.TYPE = FIELD(DICT.REC<1>,' ',1,1)
      RETURN


CHECK.COMPILE: 
      IF DICT.REC<8,1> = FIELD.NAME THEN
         RETURN
      END
      CD.COMMAND = 'CD ':FILE.NAME:' ':FIELD.NAME
      EXECUTE CD.COMMAND CAPTURING CD.OUTPUT
      IF NOT(QUIET.MODE) THEN
         NUM.LINES = COUNT(CD.COMMAND,@FM)
         FOR LINE.CTR=1 TO NUM.LINES
            CRT CD.COMMAND<LINE.CTR>
         NEXT LINE.CTR
      END
      GOSUB READ.DICT
      IF DICT.REC<8,1> # FIELD.NAME THEN
         ERROR.MESSAGE = CD.OUTPUT
         GOSUB HANDLE.ERROR
         RETURN
      END
      RETURN


HANDLE.ERROR: 
      IF NOT(QUIET.MODE) THEN
         CRT ERROR.MESSAGE
      END
      RETURN

   END
