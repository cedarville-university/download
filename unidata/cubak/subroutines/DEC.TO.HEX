      SUBROUTINE DEC.TO.HEX(RETURN.VALUE, DECIMAL.NUMBER)
*
* INFO/BASIC SUBROUTINE
* 5/17/89
* DWS
* TITLE ----- DEC.TO.HEX
*
*
* PURPOSE:CONVERT DECIMAL NUMBER TO HEXADECIMAL
*
*     Last updated by LIVE (SJOQUIST) at 11:20:48 on 05/17/1989.
*
*************************************************************************
*


*
* SET UP
*
      COMMON/DECHEX/ DECHEX.DIGITS(15), HEXNUM.VALUE(15),
                  DECHEX.LOADED
      IF NOT(DECHEX.LOADED) THEN
         FOR CTR=0 TO 9
            DECHEX.DIGITS(CTR) = CTR
         NEXT CTR
         FOR CTR=10 TO 15
            DECHEX.DIGITS(CTR) = CHAR(55+CTR)
         NEXT CTR
         FOR CTR=0 TO 15
            HEXNUM.VALUE(CTR) = 16**CTR
         NEXT CTR
         DECHEX.LOADED = 1
      END

      RETURN.VALUE = ''
      IF DECIMAL.NUMBER < 0 OR LEN(DECIMAL.NUMBER+0) > 16 THEN
         RETURN
      END
*
* CHECK HOW MANY HEX DIGITS ARE NEEDED
*
      MAX.HEX.EXP = LEN(DECIMAL.NUMBER+0) - 1
      WORK.NUMBER = DECIMAL.NUMBER
      FOR HEX.EXP=MAX.HEX.EXP TO 1 STEP -1
         HEX.VALUE = HEXNUM.VALUE(HEX.EXP)
         IF HEX.VALUE > WORK.NUMBER THEN
            IF RETURN.VALUE = '' THEN
               HEX.DIGIT = ''
            END ELSE
               HEX.DIGIT = 0
            END
         END ELSE
            TEMP.NUMBER = INT(WORK.NUMBER/HEX.VALUE)
            HEX.DIGIT = DECHEX.DIGITS(TEMP.NUMBER)
            WORK.NUMBER -= TEMP.NUMBER*HEX.VALUE
         END
         RETURN.VALUE := HEX.DIGIT
      NEXT HEX.EXP
      RETURN.VALUE := DECHEX.DIGITS(WORK.NUMBER)
      RETURN
   END
