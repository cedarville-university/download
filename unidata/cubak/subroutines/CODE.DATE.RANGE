      SUBROUTINE CODE.DATE.RANGE(RETURN.LIST, IN.CONTACT.LIST, IN.CONTACT.DATE.LIST, IN.CHK.CONTACT.LIST, BEG.DATE, END.DATE)
*
* INFO/BASIC SUBROUTINE
* 8/18/88
* DWS
* TITLE ----- CODE.DATE.RANGE
*
*
* PURPOSE: RETURNS MV ASSOC LIST (ASSOC WITH CONTACTS) OF '1'
*          FOR THOSE CONTACTS THAT ARE IN THE CHK.CONTACT.LIST
*          AND FALL IN THE DATE RANGE (BEG.DATE - END.DATE)
*
* Stamped: qb bowenr, /disk1/collive, user #2005, 28 Feb 00, 10:45AM.
*  If you want it to search for any code within a date range leave the
*  IN.CHK.CONTACT.LIST blank.
*
*     Last updated by LIVE (SJOQUIST) at 14:59:40 on 08/19/1988.
*
*************************************************************************
*

      RETURN.LIST = ''
      CONTACT.LIST = TRIM(IN.CONTACT.LIST)
      CONVERT ', ':@FM:@SM TO @VM:@VM:@VM:@VM IN CONTACT.LIST
      CONTACT.DATE.LIST = TRIM(IN.CONTACT.DATE.LIST)
      CONVERT ', ':@FM:@SM TO @VM:@VM:@VM:@VM IN CONTACT.DATE.LIST
      CHK.CONTACT.LIST = TRIM(IN.CHK.CONTACT.LIST)
      IF CHK.CONTACT.LIST NE '' THEN
         CONVERT ', ':@FM:@SM TO @VM:@VM:@VM:@VM IN CHK.CONTACT.LIST
      END
      LOOP
         REMOVE CONTACT FROM CONTACT.LIST SETTING DELIM.CON
         REMOVE CONTACT.DATE FROM CONTACT.DATE.LIST SETTING DUMMY
         RESULT = @FALSE
         IF CONTACT.DATE >= BEG.DATE AND CONTACT.DATE <= END.DATE THEN
            IF CHK.CONTACT.LIST NE '' THEN
               LOCATE CONTACT IN CHK.CONTACT.LIST<1,1> SETTING POS THEN
                  RESULT = @TRUE
               END
            END ELSE
               RESULT = @TRUE
            END
         END
         RETURN.LIST := @VM:RESULT
      UNTIL DELIM.CON = 0
      REPEAT
      DEL RETURN.LIST<1,1>
      CONVERT '0' TO '' IN RETURN.LIST
      RETURN
   END
