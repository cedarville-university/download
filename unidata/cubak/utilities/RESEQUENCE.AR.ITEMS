*
* INFO/BASIC PROGRAM
* 3/2/90
* DLR
* TITLE ----- RESEQUENCE.AR.ITEMS
*
*
* MODULE :
* PURPOSE: RESEQUENCE AR.ITEMS AFTER DELETING ONE OR MORE ITEMS
*          FOR A PARTICULAR PERSON
*
*     Last updated by LIVE (ROTMAN) at 08:46:27 on 10/03/1992.
*
*************************************************************************
*


      ERROR.RET = ''
      ERROR.MSG = ''
      F.AR.FILE = ''
      CALL OPEN.FILE(F.AR.FILE,'AR.FILE',ERROR.RET,ERROR.MSG)
      F.AR.ITEMS = ''
      CALL OPEN.FILE(F.AR.ITEMS,'AR.ITEMS',ERROR.RET,ERROR.MSG)

      PROMPT ''
      NO.PAUSE = @(0,0)
      DONE = @FALSE
      LOOP
         READNEXT LIST.KEY ELSE
            DONE = @TRUE
         END
      UNTIL DONE
         GOSUB PROCESS.AR.FILE.KEY
      REPEAT
      STOP


PROCESS.AR.FILE.KEY: 
      AR.FILE.KEY = FIELD(LIST.KEY,'*',1,2)
      READU AR.FILE.REC FROM F.AR.FILE,AR.FILE.KEY LOCKED
         CRT AR.FILE.KEY:' LOCKED BY USER ':STATUS():'.'
         RETURN
      END ELSE
         CRT AR.FILE.KEY:' NOT FOUND'
         RELEASE F.AR.FILE, AR.FILE.KEY
         RETURN
      END
      AR.FILE.COUNTER = AR.FILE.REC<1>
      CRT AR.FILE.KEY:' ':AR.FILE.COUNTER
      NEW.COUNTER = 0
      FOR WHICH.ITEM = 1 TO AR.FILE.COUNTER
         OLD.AR.ITEMS.KEY = AR.FILE.KEY:'*':WHICH.ITEM
         READ AR.ITEMS.REC FROM F.AR.ITEMS, OLD.AR.ITEMS.KEY THEN
            NEW.COUNTER += 1
            NEW.AR.ITEMS.KEY = AR.FILE.KEY:'*':NEW.COUNTER
            WRITE AR.ITEMS.REC ON F.AR.ITEMS, NEW.AR.ITEMS.KEY
            IF NEW.COUNTER < WHICH.ITEM THEN
               CRT 'DELETING ':OLD.AR.ITEMS.KEY
               DELETE F.AR.ITEMS, OLD.AR.ITEMS.KEY
            END
         END ELSE
            CRT '     ITEM ':OLD.AR.ITEMS.KEY:' MISSING FROM AR.ITEMS'
         END
      NEXT WHICH.ITEM
      AR.FILE.REC<1> = NEW.COUNTER
      CRT '     OLD COUNTER, NEW COUNTER ':AR.FILE.COUNTER,NEW.COUNTER
      WRITE AR.FILE.REC ON F.AR.FILE, AR.FILE.KEY
      RETURN

   END
