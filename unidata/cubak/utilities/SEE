*
* UNIBASIC PROGRAM
* 05/09/95
* DLR
* TITLE -----  SEE
*
* PURPOSE:  DISPLAY A VOC RECORD TO THE SCREEN WITHOUT USING
*           THE EDITOR.  ('.L' ONLY WORKS ON S/PA ITEMS)
*
* Stamped: pty/ttyt7 root, /disk1/collive, user #26613, 09 May 95, 04:54PM.
*
*************************************************************************
*


      COMMAND.LINE = @SENTENCE
      GOSUB LOAD.PARAMETERS
      GOSUB GET.ITEM
      GOSUB DISPLAY.ITEM
      STOP


LOAD.PARAMETERS: 
      NUM.PARAMS = 0
      PARAM.QUOTED = ''
      PARAM.STR = ''
      CALL PARSE.LINE(COMMAND.LINE,PARAM.STR,PARAM.QUOTED,NUM.PARAMS)
      IF PARAM.STR<1> = 'RUN' THEN
         STARTING.PARAM = 4
      END ELSE
         STARTING.PARAM = 2
      END

      IF NUM.PARAMS = STARTING.PARAM THEN
         FILE.NAME = 'VOC'
         ITEM.NAME = PARAM.STR<STARTING.PARAM>
      END ELSE
         FILE.NAME = PARAM.STR<STARTING.PARAM>
         ITEM.NAME = PARAM.STR<STARTING.PARAM + 1>
      END
      IF ITEM.NAME = '' THEN
         STOP 'You must specify an item to SEE.'
      END
      RETURN


GET.ITEM: 
      OPEN '',FILE.NAME TO F.FILE.NAME ELSE
         STOP 'Unable to open ':FILE.NAME:'.'
      END
      READ ITEM.REC FROM F.FILE.NAME, ITEM.NAME ELSE
         STOP 'Unable to read ':ITEM.NAME:' from ':FILE.NAME:'.'
      END
      RETURN


DISPLAY.ITEM: 
      IF ITEM.REC = '' THEN
         STOP ITEM.NAME:' in file ':FILE.NAME:' is empty.'
      END
      PRINT.LINE = FILE.NAME:' ':ITEM.NAME
      CRT PRINT.LINE
      LINE.COUNT = COUNT(ITEM.REC,@FM) + (ITEM.REC#'')
      FOR WHICH.LINE = 1 TO LINE.COUNT
         PRINT.LINE = FMT(WHICH.LINE,'3"0"R')
         PRINT.LINE := ': '
         PRINT.LINE := ITEM.REC<WHICH.LINE>
         CRT PRINT.LINE
      NEXT WHICH.LINE
      RETURN


   END
