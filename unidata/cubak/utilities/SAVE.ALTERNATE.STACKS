*
* INFO/BASIC PROGRAM
* 3/13/90
* DWS
* TITLE ----- SAVE.ALTERNATE.STACKS
*
*
* PURPOSE: SAVE COMMAND "STACKS" FROM PROMPT.STACK (USED IN ON.EXIT PARAGRAPH)
*
* Stamped: pty/ttyp8 sjoquist, /disk1/coltest, user #25003, 25 Jun 94, 04:54PM.
* Updated for HP-UX
*     Last updated by LIVE (SJOQUIST) at 08:26:43 on 03/13/1990.
*
*************************************************************************
*

$INCLUDE LOCAL.SOURCE I_PROMPT.STACK.COMMON

*
*
* MAIN CONTROL
*

      GOSUB SET.UP
      IF PRSTACK.PROG.NAMES = '' OR PRSTACK.PROG.NAMES = '0' THEN
         NUM.STACKS = 0
      END ELSE
         NUM.STACKS = COUNT(PRSTACK.PROG.NAMES,@FM) + 1
      END
      FOR STACK.CTR=1 TO NUM.STACKS
         WHICH.PROG.NAME = PRSTACK.PROG.NAMES<STACK.CTR>
         IF WHICH.PROG.NAME = CURRENT.PROG.NAME THEN
            WHICH.PROG.STACK = PROG.STACK
         END ELSE
            WHICH.PROG.STACK = PRSTACK.PROG.STACK<STACK.CTR>
         END
         GOSUB PROCESS.STACK
      NEXT STACK.CTR
*      WRITE ALT.STACKWRITE.REC TO F.VOC, 'ALTERNATE.STACKWRITE'
      STOP



*
*
* SET UP
*
SET.UP:
      ON.ERROR.RETURN = @FALSE
      ERRMSG = ''
      F.VOC = ''
      CALL OPEN.FILE(F.VOC, 'VOC', ON.ERROR.RETURN, ERRMSG)
      F.SAVEDLISTS = ''
      CALL OPEN.FILE(F.SAVEDLISTS, '&SAVEDLISTS&', ON.ERROR.RETURN, ERRMSG)
      READ ALT.STACKWRITE.REC FROM F.VOC, 'ALTERNATE.STACKWRITE' ELSE
         ALT.STACKWRITE.REC = ''
      END
      RETURN


*
*
* PROCESS STACK
*
PROCESS.STACK:
      OKAY.TO.WRITE = @TRUE
      LOCATE WHICH.PROG.NAME IN ALT.STACKWRITE.REC<2,1> SETTING NAME.POS THEN
         ALT.STATUS = OCONV(ALT.STACKWRITE.REC<3,NAME.POS>,'MCU')
         IF ALT.STATUS = 'OFF' THEN
            OKAY.TO.WRITE = @FALSE
         END
      END ELSE
         ALT.STACKWRITE.REC<2,NAME.POS> = WHICH.PROG.NAME
         ALT.STACKWRITE.REC<3,NAME.POS> = 'ON'
      END
      IF OKAY.TO.WRITE THEN
         CONVERT @VM TO @FM IN WHICH.PROG.STACK
         STACK.NAME = '&&':WHICH.PROG.NAME:'.':@LOGNAME:'.':@USERNO
         WRITE WHICH.PROG.STACK TO F.SAVEDLISTS, STACK.NAME
      END
      RETURN

   END
