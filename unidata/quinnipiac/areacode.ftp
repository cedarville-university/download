570 LINES
*-------------------------- FTP SUBMISSION FORM --------------------------
*--- PROGRAM NAME...: Change telephone area codes
*--- 40-CHAR DESC...: Change telephone area codes
*--- KEYWORDS.......: PHONE AREA CODE SPLIT CHANGE
*--- SUBMISSION DATE: 6 September, 1996
*--- COMPANY NAME...: Quinnipiac College
*--- CONTACT NAME...: Eric Spitzner
*--- CONTACT PHONE#.: 203/281-8615
*--- CONTACT EMAIL..: spitzner@quinnipiac.edu
*--- LONG DESC......: A program, subroutine & procedure to facilitate
*---                : changing telephone area codes when an area is split.
*---                :
*---                :
*---                :
*-------------------------------------------------------------------------
FILE/RECORD INFORMATION:
  1 CUSTOM.SOURCE...................... Q24.AREACODE.README.TXT
  2 CUSTOM.SOURCE...................... Q24.CHANGE.AREA.CODES
  3 CUSTOM.SOURCE...................... Q24.CHG.AREA.CODE
  4 CUSTOM.SOURCE...................... Q24.CHG.AREA.CODE.CASE
 ##### FILE CUSTOM.SOURCE #########################    4 RECORDS ######
 ======== RECORD Q24.AREACODE.README.TXT ==========   111 FIELDS ======
 The process of changing telephone area codes is NOT a production proce
Css.
 But whenever an area code is split by the phone company the change can
 readily be made via the following three pieces:

 1.   Program Q24.CHANGE.AREA.CODES in CUSTOM.SOURCE will prompt for fi
Cle and
      field names, then update the selected records by calling the foll
Cowing
      subroutine.

 2.   Subroutine Q24.CHG.AREA.CODE in CUSTOM.SOURCE will return a new
      telephone number derived from a telephone number passed to it.  I
Cf the
      field passed to the subroutine is multi-valued the field returned
C will
      also be multi-valued; each value will be processed identically.
      a.   If the length of the numeric portion of the input telephone
Cnumber
           is less than seven, the returned value is the passed value.
      b.   The passed telephone number is parsed into line number, cent
Cral
           office, area code and prefix, e.g. "1-", plus delimiters, wo
Crking
           from right to left.
      c.   If the passed telephone number has no area code, a local are
Ca code
           is assigned.
      d.   If the passed area code is equal to the hard-coded old area,
C the
           new area code is determined by a source procedure $included
Cinto
           the subroutine.
      e.   If the new area code is equal to the old area code, the new
           telephone number is copied from the passed telephone number.
           Otherwise the new telephone number is formed from the passed
           prefix, the new area code, delimiter, central office, delimi
Cter and
           line number.  The delimiters are the hyphen which optionally
           separates central office and line number (ooo-llll) and whic
Ch
           optionally separates area code and central office (aaa/ooo-l
Clll).
           If the passed number had no area code the office/line delimi
Cter is
           used to delimit the area/office, i.e. ooo-llll is returned a
Cs
           aaa-ooo-llll.

 3.   The source procedure to be $included into the subroutine is a Uni
CBasic
      code fragment which sets W.NEW.AREA to the appropriate numeric va
Clue
      whenever W.CENTRAL.OFFICE indicates a change, or sets W.NEW.AREA
Cto
      W.OLD.AREA otherwise.  We coded this as a case statement:
           BEGIN CASE
                CASE W.CENTRAL.OFFICE EQ ### W.NEW.AREA = ###
                CASE W.CENTRAL.OFFICE EQ ### W.NEW.AREA = ###
                CASE 1 W.NEW.AREA = W.OLD.AREA
           END CASE
      The value of W.NEW.AREA may be the same in each case or differ de
Cpending
      upon W.CENTRAL.OFFICE.

 We created the above source procedure using the central offices found
Con the
 World Wide Web at URL http://www1.mhv.net/~Bruderhof/npanxx.htm (Area
CCode
 Split Details - a public service of Rifton Enterprises).  The process
Cwe used
 was:

 1.   Point our Web browser to URL http://www1.mhv.net/~Bruderhof/npanx
Cx.htm

 2.   Click on an applicable area code split

 3.   Save the page in any convenient directory of the PC under any val
Cid name

 4.   Upload the saved page to the CUSTOM.SOURCE directory on the Datat
Cel
      system

 5.   Edit the page to form a case statement
      a.   Delete the lines following the last central office
      b.   Delete the lines preceding the first central office
      c.   Split the multi-column list of central offices.  We used BN#
C##/ /
           in AE (the Alternative Editor) with ### being an appropriate
           repetition number.
      d.   Append W.NEW.AREA = ### to each line, where ### is the new a
Crea
           code, e.g.  "A###  W.NEW.AREA = 860"
      e.   Insert "CASE W.CENTRAL OFFICE EQ " at the beginning of each
Cline,
           e.g.  "C###//CASE W.CENTRAL.OFFICE EQ "
      f.   Add "CASE 1 W.NEW.AREA = W.OLD.AREA" and "END CASE" to the
           bottom of the file
      g.   Add "BEGIN CASE" to the top of the file.

 6.   CHECK YOUR WORK.  We found a number of exchanges listed in the br
Cochure
      provided by Southern New England Telephone which were not in the
CWeb
      page (possibly due to the time frame in which we worked vs. when
CRifton
      Enterprises did.

 7.   Change the values of K.LOCAL.AREA, K.NEW.AREA and K.OLD.AREA in
      Q24.CHG.AREA.CODE in Q24.CHG.AREA.CODE.

 8.   Compile and catalog Q24.CHG.AREA.CODE

 You may test these pieces by using Q24.CHG.AREA.CODE in an I-descripto
Cr, or
 in a UniQuery list.  E.g. LIST PEOPLE P.PHONE EVAL "SUBR( Q24.CHG.AREA
C.CODE',
 P.PHONE)" COL.HDG "New Phone" FMT "12R"

 Run Q24.CHANGE.AREA.CODES to update the telephone number fields in eac
Ch file
 which contains telephone numbers.  We found (with Benefactor 4.0 and
 Colleague 13.7 + HR) the following:
 1.   ADDRESS
      a.   ADDRESS.PHONES
 2.   ADREL
      a.   LOCAL.PHONE
 3.   ATP.HOLD
      a.   PHONE
 4.   FA.FILE
      a.   STAF.PERM.PHONE
 5.   FACULTY
      a.   TELEPHONE
 6.   PEOPLE
      a.   AR.PHONE
      b.   BUS.PHONE
      c.   E.PHONE
      d.   L.PHONE
      e.   O.PHONE
      f.   P.PGS.PHONE
      g.   P.PHONE
 7.   REG.ADDR.HOLD
      a.   RAH.ADDRESS.PHONES
      b.   RAH.LOCAL.PHONE
 ======== END OF RECORD ===============================================
 ======== RECORD Q24.CHANGE.AREA.CODES ============   269 FIELDS ======
   PROGRAM Q24.CHANGE.AREA.CODES
 * 18 July, 1996
 * UniBasic program
 * Eric Spitzner, CDP
 * Q24.CHANGE.AREA.CODES
 * 13.7
 * Update specified field(s) in specified file(s) with new area code(s)
C.
 * Pass each of the specified fields to the subroutine Q24.CHG.AREA.COD
CE
 * then update the record.
 *
 * Last updated by ...
 * Stamped: t1 spitzner, /data4/test/coll/coltest, user #10358, 04 Sep
C96, 03:31PM.
 *
   EQUATE DD.F1                          TO    1;*      3L     S
   EQUATE DD.LOC                         TO    2;*      13R    S
   EQUATE DD.CONV                        TO    3;*      4L     S
   EQUATE DD.NAME                        TO    4;*      15L    S
   EQUATE DD.FORMAT                      TO    5;*      6L     S
   EQUATE DD.SM                          TO    6;*      2L     S
   EQUATE DD.ASSOC                       TO    7;*      10T    S

   DIMENSION W200.RECORD(300)



 P000.CHANGE.AREA.CODES:
   PROMPT ""

   LOOP
     GOSUB P100.IDENTIFY.SOURCE:
   UNTIL W000.FILENAME EQ "" DO
     GOSUB P200.UPDATE.FILE:
   REPEAT

   STOP



 P100.IDENTIFY.SOURCE:
   CRT @(-1) : @(31) : "UPDATE AREA CODES"

   LOOP
     GOSUB P110.GET.FILENAME:
     W000.FIELD.NAME = ""
     W000.FIELD.LOCATION = ""
     W100.FIELD.COUNT = 0
     W100.CONFIRMATION = ""
     IF W000.FILENAME EQ "" THEN
       NULL
     END ELSE
       LOOP
         GOSUB P120.GET.FIELD.NAME:
       UNTIL W100.FIELD EQ ""
       REPEAT
       CLOSE F.DICT.FILENAME
       GOSUB P130.CONFIRM:
     END
   UNTIL W000.FILENAME EQ "" OR W100.CONFIRMATION[1, 1] EQ "Y"
   REPEAT

   RETURN



 P110.GET.FILENAME:
   W000.FILENAME = ""
   W110.FILE.IDENTIFIED = ""

   LOOP
     CRT @(0, 2) : @(-3) : "Enter File Name to process, or just press [
CNL] to exit: " : @(57, 2) :
     INPUT W000.FILENAME
     CRT @(57, 2) : W000.FILENAME : @(-3)

     IF W000.FILENAME EQ "" THEN
       W110.FILE.IDENTIFIED = 1
     END ELSE
       OPEN "DICT", W000.FILENAME READONLY TO F.DICT.FILENAME THEN
         W110.FILE.IDENTIFIED = 1
       END ELSE
         INPUTERR "I cannot open the DICT of file '" : W000.FILENAME :
C"'"
       END
     END
   UNTIL W110.FILE.IDENTIFIED
   REPEAT

   RETURN



 P120.GET.FIELD.NAME:
   LOOP
     W120.FIELD.IDENTIFIED = ""
     CRT @(0, W100.FIELD.COUNT + 3) : "Enter Field Name to process, or
Cjust press [NL] when done: " : @(60, W100.FIELD.COUNT + 3) :
     INPUT W100.FIELD
     CRT @(60, W100.FIELD.COUNT + 3) : W100.FIELD : @(-3)

     IF W100.FIELD EQ "" THEN
       W120.FIELD.IDENTIFIED = 1
     END ELSE
       READ R.DICT.FILENAME FROM F.DICT.FILENAME, W100.FIELD THEN
         IF R.DICT.FILENAME<DD.F1>[1, 1] EQ "D" THEN
           W000.FIELD.LOCATION<1, W100.FIELD.COUNT + 1> = ICONV(R.DICT.
CFILENAME<DD.LOC>, "MCN")
           IF W000.FIELD.LOCATION<1, W100.FIELD.COUNT + 1> GT 0 THEN
             W100.FIELD.COUNT += 1
             W000.FIELD.NAME<1, W100.FIELD.COUNT> = W100.FIELD
             W120.FIELD.IDENTIFIED = 1
           END ELSE
             INPUTERR "'" : W100.FIELD : "' is a key field in file " :
CW000.FILENAME
           END
         END ELSE
           INPUTERR "'" : W100.FIELD : "' is not a data field in file "
C : W000.FILENAME
         END
       END ELSE
         INPUTERR "'" : W100.FIELD : "' is not a field in file " : W000
C.FILENAME
       END
     END
   UNTIL W120.FIELD.IDENTIFIED
   REPEAT

   RETURN



 P130.CONFIRM:
   CRT @(0, 2) : @(-3) : @(29, 2) : "Update file " : W000.FILENAME
   W100.FIELD.COUNT = 1

   LOOP
     W100.FIELD = FIELD(W000.FIELD.NAME, @VM, W100.FIELD.COUNT, 1)
   WHILE COL2() DO
     CRT @(31, W100.FIELD.COUNT + 2) : "Field  " : FMT(W000.FIELD.LOCAT
CION<1, W100.FIELD.COUNT>, "3R") : " (" : W100.FIELD : ")"
     W100.FIELD.COUNT += 1
   REPEAT

   CRT @(23, W100.FIELD.COUNT + 4) : "Is that what you want? [Y,N,END]:
C " : @(-3) :
   INPUT W100.CONFIRMATION :
   W100.CONFIRMATION = ICONV(W100.CONFIRMATION, "MCU")
   CRT @(57, W100.FIELD.COUNT + 4) : W100.CONFIRMATION : @(-3)

   IF W100.CONFIRMATION EQ "END" THEN
     W000.FILENAME = ""
   END

   RETURN



 P200.UPDATE.FILE:
   GOSUB P210.BEGIN.UPDATE:

   LOOP
   WHILE W200.RECORD.AVAIL
     GOSUB P220.UPDATE.RECORD:
   REPEAT

   GOSUB P230.END.UPDATE:

   RETURN



 P210.BEGIN.UPDATE:
   W200.RECORDS.READ = 0
   W200.RECORDS.UPDATED = 0
   W200.RECORD.AVAIL = ""

   OPEN "", W000.FILENAME TO F.FILENAME THEN
     W210.SELECT.STATEMENT = "SELECT " : W000.FILENAME : " WITH " : W00
C0.FIELD.NAME<1, 1> : " NE ''"

     W200.FIELD.COUNT = 2
     LOOP
       W200.FIELD = FIELD(W000.FIELD.NAME, @VM, W200.FIELD.COUNT, 1)
     WHILE COL2() DO
       W210.SELECT.STATEMENT := " OR WITH " : W200.FIELD : " NE ''"
       W200.FIELD.COUNT += 1
     REPEAT

     CRT @(0, 2) : @(-3) : @(17, 2) : "S E L E C T I N G   -   P L E A
CS E   W A I T"
 !    CRT @(0, 4) : W210.SELECT.STATEMENT
     EXECUTE W210.SELECT.STATEMENT
     W000.RECORDS.SELECTED = @SYSTEM.RETURN.CODE

     IF W000.RECORDS.SELECTED GT 0 THEN
       W200.RECORD.AVAIL = 1
       GOSUB P221.GET.NEXT.RECORD:
     END
   END ELSE
     CRT "I cannot open file '" : W000.FILENAME : "'"
   END

   RETURN



 P220.UPDATE.RECORD:
   W220.RECORD.CHANGED = ""
   W220.FIELD.COUNTER = 1

   LOOP
     W220.FIELD.LOC = FIELD(W000.FIELD.LOCATION, @VM, W220.FIELD.COUNTE
CR, 1)
   WHILE COL2()
     GOSUB P222.CHANGE.AREA.CODE:
     W220.FIELD.COUNTER += 1
   REPEAT

   IF W220.RECORD.CHANGED THEN
     GOSUB P223.PUT.RECORD:
   END ELSE
     RELEASE F.FILENAME, W200.RECORD.ID
   END

   GOSUB P221.GET.NEXT.RECORD:

   RETURN



 P221.GET.NEXT.RECORD:
   READNEXT W200.RECORD.ID THEN
     W200.RECORDS.READ += 1
     CRT @(25, 7) : "Now processing " : W200.RECORD.ID : " (" : W200.RE
CCORDS.READ : "/" : W000.RECORDS.SELECTED : ")" : @(-4)
     MATREADU W200.RECORD FROM F.FILENAME, W200.RECORD.ID ELSE
       MAT W200.RECORD = ""
       CRT @(15, 7) : "No record in " : W200.FILENAME : " for ID '" : W
C200.RECORD.ID : "'"
     END
   END ELSE
     W200.RECORD.AVAIL = ""
   END

   RETURN



 P222.CHANGE.AREA.CODE:
   W222.NEW.PHONE = ""
   CALL Q24.CHG.AREA.CODE(W222.NEW.PHONE, W200.RECORD(W220.FIELD.LOC))

   IF W222.NEW.PHONE EQ W200.RECORD(W220.FIELD.LOC) THEN
     NULL
   END ELSE
     W200.RECORD(W220.FIELD.LOC) = W222.NEW.PHONE
     W220.RECORD.CHANGED = 1
   END

   RETURN



 P223.PUT.RECORD:
   MATWRITE W200.RECORD ON F.FILENAME, W200.RECORD.ID
   W200.RECORDS.UPDATED += 1

   RETURN



 P230.END.UPDATE:
   CRT @(0, 4) : @(-3)
   CRT @(25, 6) : FMT(OCONV(W200.RECORDS.READ, "MD0,"), "7R") : " " : W
C000.FILENAME : " records read" : @(-7)
   CRT @(25, 7) : FMT(OCONV(W200.RECORDS.UPDATED, "MD0,"), "7R") : " re
Ccords updated"
   CLOSE F.FILENAME
   CRT @(24, 9) : "Press [NL] to continue" :
   INPUT W230.NL

   RETURN



 END
 ======== END OF RECORD ===============================================
 ======== RECORD Q24.CHG.AREA.CODE ================    81 FIELDS ======
   SUBROUTINE Q24.CHG.AREA.CODE(L.NEW.PHONE, L.OLD.PHONE)
 * 18 July, 1996
 * UniBasic subroutine
 * Eric Spitzner, CDP
 * Q24.CHG.AREA.CODE
 * 13.7
 * Return phone number with new area code
 *
 *
 *
 * Last updated by ...
 * Stamped: t1 spitzner, /data4/test/coll/coltest, user #10358, 04 Sep
C96, 03:40PM.
 *



   EQUATE K.LOCAL.AREA LITERALLY "203"
   EQUATE K.NEW.AREA LITERALLY "860"
   EQUATE K.OLD.AREA LITERALLY "203"

   L.NEW.PHONE = ""
   W.IDX = 0

   LOOP
     W.IDX += 1
     W.OLD.PHONE = FIELD(L.OLD.PHONE, @VM, W.IDX, 1)
   WHILE COL2() DO
     W.TEMP.PHONE = ICONV(W.OLD.PHONE, "MCN") + 0
     W.TEMP.PHONE.LENGTH = LEN(W.TEMP.PHONE)

     IF W.TEMP.PHONE.LENGTH GE 7 THEN
       W.LINE.NUMBER = W.TEMP.PHONE[W.TEMP.PHONE.LENGTH - 3, 4]
       W.CENTRAL.OFFICE = W.TEMP.PHONE[W.TEMP.PHONE.LENGTH - 6, 3]
       IF W.TEMP.PHONE.LENGTH GE 10 THEN
         W.OLD.AREA = W.TEMP.PHONE[W.TEMP.PHONE.LENGTH - 9, 3]
       END ELSE
         W.OLD.AREA = K.LOCAL.AREA
       END
       IF W.OLD.AREA EQ K.OLD.AREA THEN
         $INCLUDE CUSTOM.SOURCE Q24.CHG.AREA.CODE.CASE
       END ELSE
         W.NEW.AREA = W.OLD.AREA
       END

       IF W.NEW.AREA EQ W.OLD.AREA THEN
         W.NEW.PHONE = W.OLD.PHONE
       END ELSE
         W.OLD.PHONE.LENGTH = LEN(W.OLD.PHONE)
         W.DELIMITER.2 = ICONV(W.OLD.PHONE[W.OLD.PHONE.LENGTH - 4, 1],
C"MC/B")
         W.DELIMITER.2.LENGTH = LEN(W.DELIMITER.2)
         IF (W.OLD.PHONE.LENGTH - W.DELIMITER.2.LENGTH) GT 7 THEN
           W.DELIMITER.1 = ICONV(W.OLD.PHONE[W.OLD.PHONE.LENGTH - 7 - W
C.DELIMITER.2.LENGTH, 1], "MC/B")
           W.DELIMITER.1.LENGTH = LEN(W.DELIMITER.1)
         END ELSE
           W.DELIMITER.1 = W.DELIMITER.2
           W.DELIMITER.1.LENGTH = 0
         END
         IF (W.OLD.PHONE.LENGTH - W.DELIMITER.2.LENGTH - W.DELIMITER.1.
CLENGTH) GT 10 THEN
           W.NEW.PHONE = W.OLD.PHONE[1, W.OLD.PHONE.LENGTH - 10 - W.DEL
CIMITER.2.LENGTH - W.DELIMITER.1.LENGTH]
         END ELSE
           W.NEW.PHONE = ""
         END
         W.NEW.PHONE := W.NEW.AREA
         W.NEW.PHONE := W.DELIMITER.1
         W.NEW.PHONE := W.CENTRAL.OFFICE
         W.NEW.PHONE := W.DELIMITER.2
         W.NEW.PHONE := W.LINE.NUMBER
       END
     END ELSE
       W.NEW.PHONE = W.OLD.PHONE
     END

     L.NEW.PHONE<1, W.IDX> = W.NEW.PHONE

   REPEAT

   RETURN



 END
 ======== END OF RECORD ===============================================
 ======== RECORD Q24.CHG.AREA.CODE.CASE ===========    18 FIELDS ======
 *
 * 18 July, 1996
 * UniBasic code fragment
 * Eric Spitzner, CDP
 * Q24.CHG.AREA.CODE.CASE
 * n/a
 * UniBasic code fragment which, when $included into Q24.CHG.AREA.CODE,
 * will replace W.NEW.AREA by an appropriate value depending upon the
 * value of W.CENTRAL.OFFICE.
 *
 * Last updated by ...
 * Stamped: t1 spitzner, /data4/test/coll/coltest, user #10358, 04 Sep
C96, 03:40PM.
 *
   BEGIN CASE
     CASE W.CENTRAL.OFFICE EQ 001 W.NEW.AREA = 001
     CASE W.CENTRAL.OFFICE EQ 002 W.NEW.AREA = 002
     CASE 1 W.NEW.AREA = W.OLD.AREA
   END CASE
 ======== END OF RECORD ===============================================
 ### END OF FILE ######################################################
